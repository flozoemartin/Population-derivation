********************************************************************************

* Cleaning the HES Maternity data

* Author: Harriet Forbes (adapted for antidepressant project by Flo Martin)

* Date: 25/11/2022

********************************************************************************

* Datasets generated by this do-file

* 		- $Datadir\derived_data\hes_deliveries_final.dta 
* 		- $Datadir\derived_data\hes_losses_final.dta

/* Data note - using HES Maternity to supplement unknown delivery outcomes (delivery based on a late/third trimester record and unknown outcomes) and other HES APC to supplement unknown loss outcomes (unspecified loss, probable TOP and unknown outcomes)

	- Only use HES Maternity dates for outcomes that are truly unknown i.e., unknown outcomes, so as to not undermine the algorithm too much on a subset of unspecified delivery outcomes only among those with linked data

********************************************************************************/

	use "$Tempdatadir\pregreg_hesoutcomesstep.dta", clear

* Add information required:
	
	merge 1:1 patid pregid using "$Datadir\formatted_linked_data\pregnancyregister.dta", keep(match) nogen
	merge m:1 patid using "$Datadir\formatted_linked_data\linkage_eligibility.dta", keep(master match) keepusing(hes_apc_e) nogen
	gen linkage_hes = 1 if hes_apc_e==1
	recode linkage_hes .=0
	
	tab linkage_hes // 47% with linked data
	
	save "$Tempdatadir\pregreg_hesoutcomesstep_extra_vars.dta", replace
	
* Create datasets containing unique patids combinations
	* (occurs because there can be multiple pregnancies for one mum)

	bysort patid: gen bign=_N /*unique patids*/
	tab bign /*max 25 pregnancies*/
	sort patid pregstart
	summ bign
	local pregmax = r(max)
	drop big

	forvalues x=1/`pregmax' {

		use "$Tempdatadir\pregreg_hesoutcomesstep_extra_vars.dta", clear
		bysort patid (pregid): keep if _n==`x' /*unique patids*/
		save "$Tempdatadir\pregreg_hesoutcomesstep_extra_vars_`x'.dta", replace
	
	}
	
********************************************************************************	

* DELIVERIES

* Load in HES Maternity data

	use "$Datadir\formatted_linked_data\hes_maternity.dta", clear
	
* General cleaning of variables using the NHS HES Data Dictionary - https://digital.nhs.uk/data-and-information/data-tools-and-services/data-services/hospital-episode-statistics/hospital-episode-statistics-data-dictionary
	
* Number of babies delivered at the end of a single pregnancy
	
	tab numbaby_num
	drop numbaby
	rename numbaby_num numbaby
	
* Method used to initiate labour

	tab delonset
	tab delonset, nolab
	
	recode delonset 8=. 
	
	tab delonset
	
* Delivery method
	
	tab delmeth_num
	drop delmeth
	rename delmeth_num delmeth
	
* Sex of the baby

	drop sexbaby
	rename sexbaby_num sexbaby
	
	label define sexbaby_lb 1"Male" 2"Female"
	label values sexbaby sexbaby_lb
	
	recode sexbaby 0=. 3=. 4=. 5=. 6=. 7=. 7=. 8=. 9=. 
	label variable sexbaby "Sex of baby"
	
	tab sexbaby
	
* Number of weeks of completed gestation

	tab gestat
	
	replace gestat =. if gestat<20
	
* Sequence in which the baby was born

	tab birordr_num
	drop birordr
	rename birordr_num birordr
	
	recode birordr 8=.
	
* Method by which the baby was resuscitated - detail not important here

	tab biresus
	tab biresus, nol
	recode biresus 8=.

	tab biresus
	
* Stillborn

	tab birstat
	
* Birthweight

	tab birweit
	recode birweit -1=.
	
********************************************************************************

* Generate a variable that confirms whether a live or stillbirth occurred

	gen birth_status =.
	replace birth_status = 1 if birstat==1
	replace birth_status = 2 if birstat==2 | birstat==3 | birstat==4
	
	label define birth_status_lb 1"Live born" 2"Stillborn"
	label values birth_status birth_status_lb
	
	tab birth_status
	
* Create potential delivery dates: 
	* Generate antedur=number of days from epistart to delivery; postdur=number of days from delivery to epiend
	* Add number of antenatal days of stay to epistart

	gen del_date_ant = epistart_num + antedur
	
	* Subtract number of postnatal days of stay from epiend 

	gen del_date_post = epiend_num - postdur
	format del_date* %td

* Keep records with sufficient evidence of delivery occurring
* Keep episodes with 2 or more valid delivery-related fields (del*) in Maternity record
	* Generate flag

	egen no_del_fields = rownonmiss(epiend_num epistart_num numbaby birordr birth_status biresus sexbaby birweit delonset gestat delmeth)
	gen hes = 1 if no_del_fields>=1
	keep if hes==1
	bysort patid epikey: egen stillbirth=max(birth_status)
	bysort patid epikey: egen gestwks=max(gestat)
	
	label define stillbirth_lb 1"Live born" 2"Stillborn"
	label values stillbirth stillbirth_lb

	keep patid del_date* epistart_num hes epikey stillbirth gestwks

	duplicates drop
	duplicates report patid epikey
	
	save "$Tempdatadir\hes_maternity_all.dta", replace
	
********************************************************************************

* Load in HES Procedure data

	use "$Datadir\formatted_linked_data\hes_procedures.dta", clear
	
* Merge with codelist (checked from PREPArE project)
	
	merge m:1 opcs using "$Codesdir\OPCS_delivery_or_loss_codes_CMIN", keep(match) nogen
	
	keep if category==1 | category==2 
	
* Date of each procedure was "evdate", or epistart if evdate missing

	gen del_date_opcs = evdate_num 
	replace del_date = epistart_num if del_date==.

	format del_date_opcs %td

	keep patid del_date_opcs epikey
	drop if del_date==.
	gen hes = 2
	duplicates drop
	duplicates report patid epikey
	bysort patid epikey (del_date): keep if _n==1
	duplicates report patid epikey

	save "$Tempdatadir\hes_opcs_outcomes_all.dta", replace
	
********************************************************************************

* Load in HES Admitted Patient Care data

* Identify mode of delivery

	use "$Datadir\formatted_linked_data\hes_diagnosis_epi.dta", clear
	rename icd code
	merge m:1 code using "$Codesdir\icd_mod_draft_hf.dta", keep(match)
	gen del_date_icd=epistart_num
	format del_date_icd %td
	gen hes=3
	keep patid del_date_icd epikey
	duplicates drop
	duplicates report patid epikey
	bysort patid epikey (del_date): keep if _n==1
	duplicates report patid epikey
	save "$Tempdatadir\hes_icd_deliveries.dta", replace

* Identify pregnancy outcome

	use "$Datadir\formatted_linked_data\hes_diagnosis_epi.dta", clear

	rename icd code
	merge m:1 code using "$Codesdir\ICDCode_delivery_outcome.dta", keep(match)
	
	gen del_date_icd=epistart_num
	format del_date_icd %td
	
	label define outcome_lb 1"Live born" 2"Stillborn" 3"Live and stillborn or unspecified"
	label values outcome outcome_lb

	gen hes = 3
	keep patid epistart_num epikey outcome multiple

	duplicates drop
	duplicates report patid epikey
	
	bysort patid epikey (epistart_num): keep if _n==1
	duplicates report patid epikey
	
	save "$Tempdatadir\hes_icd_outcomes_delivery.dta", replace
	
********************************************************************************

* Combine delivery data from all these sources

	use "$Tempdatadir\hes_maternity_all.dta", clear
	merge 1:1 patid epikey using "$Tempdatadir\hes_icd_deliveries.dta", keep(master match) nogen
	merge 1:1 patid epikey using "$Tempdatadir\hes_opcs_outcomes_all.dta", keep(match master) nogen
	keep if hes==1 | hes==2 | hes==3
	
	merge 1:1 patid epikey using "$Tempdatadir\hes_icd_outcomes_delivery.dta", keep(master match) nogen
	replace outcome=3 if outcome==.
	
* Generate an estimated delivery date for each retained record: taking the earliest of the potential delivery dates (based on antedur or postdur) and procedure dates. For records with no potential delivery dates or procedures, epistart was used.

	gen del_date=.
	replace del_date=min(del_date_ant, del_date_post, del_date_icd, del_date_opcs)
	drop if del_date==.
	format del_date %td
	keep patid epikey del_date hes outcome gestwks
	
* Create delivery episodes: grouping together records relating to the same delivery (records with an estimated delivery date <25 weeks  (175 days) after the initial record were deemed to relate to the same delivery)

	gen toassign = -1
	gen group = 0
	local g = 1
	qui count if toassign

	qui while r(N) > 0 {
		
		bysort patid (toassign del_date) : replace group = `g' if (del_date - del_date[1]) <= 175 & toassign
		replace toassign = 0 if group == `g'
		local ++g
		count if toassign

	}

* Keep first first in delivery episode

	bysort patid group (del_date): keep if _n==1 

	save "$Tempdatadir\hes_deliveries_all.dta", replace
	
/********************************************************************************

	To uphold the algorithm as much as possible, only change the dates for unknown outcome pregnancies and only use HES to supplement the outcome rather than the dates

********************************************************************************/

* Add in pregnancy cohort: identify deliveries based on a trimester 3 record (outcome==11) pregnancies using HES

	use "$Tempdatadir\pregreg_hesoutcomesstep_extra_vars.dta", clear
	bysort patid: gen bign=_N /*unique patids*/
	tab bign /*max 25 pregnancies*/
	sort patid pregstart
	summ bign
	local pregmax = r(max)
	drop big

	forvalues x=1/`pregmax' { // 25 pregnancies

		use "$Tempdatadir\pregreg_hesoutcomesstep_extra_vars_`x'.dta", clear
		keep if outcome==11
		drop outcome
		keep if linkage_hes==1
		merge 1:m patid using "$Tempdatadir\hes_deliveries_all.dta", keep(match)
		gen diff=del_date-(pregstart_num+28)
		sum diff, d
		keep if diff>=0 & diff<=266 
		tempfile episodes_all_`x'
		save `episodes_all_`x''

	}

	use `episodes_all_1', clear
	forvalues x=2/`pregmax' { // 25 pregnancies
	
		append using `episodes_all_`x''

	}

	bysort patid pregid (del_date): keep if _n==1 
	codebook pregid /*20,253*/
	
	tab outcome

/* Revise pregnancy start date, based on delivery date FOR DELIVERY BASED ON T3 PREGNANCY RECORD PREGNANCIES
	
	tab startsource
	tab endsource
	
	tab endsource
	replace endsource = 7 	// 7 = delivery date from HES Maternity
	replace pregend_num = del_date
	
	br pregstart_num del_date gestdays gestwks preterm_ev if gestwks!=.
	
	tab startsource // all GA from antenatal record
	replace startsource = 7 if gestwks!=. // Delivery date - gestational weeks
	
	replace pregstart_num=del_date-(gestwks*7) if gestwks!=. & ((gestwks*7)>(gestdays+7) | (gestwks*7)<(gestdays+7)) // 11,462 pregstarts amended - use gestwks*7 if that is more than a week different to gestdays - if it's within a week of gestdays then gestwks is less precise because it's just multiples of 7
	
	replace pregstart_num=pregend_num-gestdays if gestwks!=. & ((gestwks*7)<=(gestdays+7) | (gestwks*7)<=(gestdays+7)) // 0 have changed when gestdays are taken away from the new pregnancy end date
	
	replace pregstart_num=pregend_num-gestdays if gestwks==. // 5,357 chnaged when existing gestdays is taken away from the new pregnancy end date (delivery date from HES)
	
	br pregstart_num del_date gestdays gestwks preterm_ev multiple
	
	tab startsource
	
	gen gestdays_new = pregend_num - pregstart_num
	
	keep patid pregid pregstart_num pregend_num del_date outcome gestwks gestdays_new startsource endsource*/
	
	keep patid pregid del_date gestwks outcome
	
	save "$Datadir\derived_data\hes_deliveries_final_t3_record.dta", replace
	
********************************************************************************

* Add in pregnancy cohort: identify deliveries based on a late pregnancy record (outcome==12) pregnancies using HES

	use "$Tempdatadir\pregreg_hesoutcomesstep_extra_vars.dta", clear
	bysort patid: gen bign=_N /*unique patids*/
	tab bign /*max 25 pregnancies*/
	sort patid pregstart
	summ bign
	local pregmax = r(max)
	drop big

	forvalues x=1/`pregmax' { // 25 pregnancies

		use "$Tempdatadir\pregreg_hesoutcomesstep_extra_vars_`x'.dta", clear
		keep if outcome==12
		drop outcome
		keep if linkage_hes==1
		merge 1:m patid using "$Tempdatadir\hes_deliveries_all.dta", keep(match)
		gen diff=del_date-(pregstart_num+28)
		sum diff, d
		keep if diff>=0 & diff<=266 
		tempfile episodes_all_`x'
		save `episodes_all_`x''

	}

	use `episodes_all_1', clear
	forvalues x=2/`pregmax' { // 25 pregnancies
	
		append using `episodes_all_`x''

	}

	bysort patid pregid (del_date): keep if _n==1 
	codebook pregid /*12,849*/
	
	tab outcome

/* Revise pregnancy start date, based on delivery date FOR PREGNANCIES BASED ON A LATE PREGNANCY RECORD PREGNANCIES
	
	tab endsource
	replace endsource = 7
	
	replace pregend_num = del_date
	
	br pregstart_num pregend_num gestwks gestdays preterm_ev postterm_ev outcome
	
	replace startsource = 7 if gestwks!=. // delivery date - gestational weeks where available
	replace pregstart_num=del_date-(gestwks*7) if gestwks!=. & ((gestwks*7)>(gestdays+7) | (gestwks*7)<(gestdays+7)) // 7,908
	replace pregstart_num=del_date-gestdays if gestwks!=. & ((gestwks*7)<=(gestdays+7) | (gestwks*7)<=(gestdays+7))
	
	tab startsource
	
	replace startsource = 8 if gestwks==. // impute these with delivery date
	replace pregstart_num=(del_date-(280)) if startsource==8 & gestwks==. & preterm_ev!=1 & multiple!=1 & postterm_ev!=1 // 3
	replace pregstart_num=(del_date-(252)) if startsource==8 & gestwks==. & preterm_ev==1 // 22
	replace pregstart_num=(del_date-(259)) if startsource==8 & gestwks==. & multiple==1 // 0
	replace pregstart_num=(del_date-(287)) if startsource==8 & gestwks==. & postterm_ev==1 // 4
	
	tab startsource
	
	gen gestdays_new = pregend_num - pregstart_num
	
	keep patid pregid pregstart_num pregend_num del_date outcome gestwks gestdays_new startsource endsource*/
	
	tab startsource // n=293 imputed so can use gestwks where available to improve accuracy of start date
	
	replace pregstart_num = pregend_num-(gestwks*7) if gestwks!=. & startsource==1
	
	keep patid pregid pregstart_num del_date gestwks outcome
	
	save "$Datadir\derived_data\hes_deliveries_final_late_preg.dta", replace
	
********************************************************************************

* Add in pregnancy cohort: identify unknown outcome (outcome==13) pregnancies using HES

	use "$Tempdatadir\pregreg_hesoutcomesstep_extra_vars.dta", clear
	bysort patid: gen bign=_N /*unique patids*/
	tab bign /*max 24 pregnancies*/
	sort patid pregstart
	summ bign
	local pregmax = r(max)
	drop big

	forvalues x=1/`pregmax' { // 24 pregnancies

		use "$Tempdatadir\pregreg_hesoutcomesstep_extra_vars_`x'.dta", clear
		keep if outcome==13
		drop outcome
		keep if linkage_hes==1
		merge 1:m patid using "$Tempdatadir\hes_deliveries_all.dta", keep(match)
		gen diff=del_date-(pregstart_num+28)
		sum diff, d
		keep if diff>=0 & diff<=266 
		tempfile episodes_all_`x'
		save `episodes_all_`x''

	}

	use `episodes_all_1', clear
	forvalues x=2/`pregmax' { // 24 pregnancies
	
		append using `episodes_all_`x''

	}

	bysort patid pregid (del_date): keep if _n==1 
	codebook pregid /*182,377*/
	
	tab outcome

* Revise pregnancy start date, based on delivery date FOR OUTCOME UNKNOWN PREGNANCIES
	
	tab startsource
	tab endsource
	
		// no information about where the dates came from for 

	replace endsource = 7 
	replace pregend_num = del_date

	replace pregstart_num=del_date-(gestwks*7) if gestwks!=. 
	replace startsource = 7 if gestwks!=. // 130,563
	
	replace startsource = 8 if gestwks==. // 51,814
	replace pregstart_num=(del_date-(280)) if gestwks==. & preterm_ev!=1 & multiple!=1 & postterm_ev!=1 // 51,171
	replace pregstart_num=(del_date-(252)) if gestwks==. & preterm_ev==1 // 0
	replace pregstart_num=(del_date-(259)) if gestwks==. & multiple==1  // 118
	replace pregstart_num=(del_date-(287)) if gestwks==. & postterm_ev==1 // 0
	
	replace gestdays = pregend_num - pregstart_num
	
	browse
	
	keep patid pregid pregstart_num pregend_num del_date outcome gestwks gestdays startsource endsource
	save "$Datadir\derived_data\hes_deliveries_final_unknowns.dta", replace
	
********************************************************************************

* PREGNANCY LOSSES

********************************************************************************

* HES APC

	use "$Datadir\formatted_linked_data\hes_diagnosis_epi.dta", clear
	merge m:1 icd  using "$Codesdir\ICD_10_OPCS_loss_codes_with_flag_SA_TOP", keep(match)
	
	rename loss_type outcome
	lab define outcome 1 "Live birth" 2 "Stillbirth" 3 "Live birth or still birth" ///
4 "Miscarriage" 5 "TOP" 6 "Probable TOP" 7"Ectopic" 8 "Molar" 9 "Blighted ovum" 10 "Unspecified loss" 11 "Delivery based on a third trimester pregnancy record" 12 "Delivery based on a late pregnancy record4"  13 "Outcome unknown"
	lab val outcome outcome
	tab outcome, nolab
	
	* The start date of the episode of care (epistart) was used to date the pregnancy loss.

	gen loss_date_hes_apc=epistart_num
	keep patid epikey loss_date_hes_apc outcome
	duplicates drop	// n = 697
	duplicates report patid epikey 
	
	* Deal with conflicting loss codes in same pregnancy episode

	gen loss_rank=1 if outcome==7 /*ectopic*/
	replace loss_rank=2 if outcome==5 /*TOP*/
	replace loss_rank=3 if outcome==4 /*Miscarriage*/
	replace loss_rank=4 if outcome==6 /*Probable TOP*/
	replace loss_rank=5 if outcome==8 /*Molar*/
	replace loss_rank=6 if outcome==10  /*Unspec*/
	replace loss_rank=7 if outcome==9 /*blighted ovum*/
	bysort patid epikey (loss_rank): gen loss_rank_highest=outcome if _n==1
	bysort patid epikey: egen loss_rank_highest_max=max(loss_rank_highest)
	replace outcome=loss_rank_highest_max // n = 2,769 replaced
	
	keep patid epikey loss_date_hes_apc outcome
	duplicates drop
	duplicates report patid epikey

	bysort patid epikey (loss_date_hes_apc): keep if _n==1

	duplicates report patid epikey

	save "$Tempdatadir\hes_icd_losses.dta", replace

********************************************************************************

* HES Procedures
	
* The HES procedures file, using OPCS codes for early pregnancy loss procedures (category 3, S5-Table). As for deliveries, the date of the procedure (evdate) was used (when not missing), otherwise epistart was used. 

	use "$Datadir\formatted_linked_data\hes_procedures.dta", clear
	merge m:1 opcs using "$Codesdir\OPCS_loss_codes_CMIN_with_categories.dta", keep(match) nogen
	gen hes=2
	gen loss_date_hes_opcs=evdate_num
	replace loss_date_hes_opcs=epistart_num if loss_date_hes_opcs==.
	keep patid epikey loss_date_hes_opcs outcome
	duplicates drop	// n = 1,028
	duplicates report patid epikey

* Deal with conflicting loss codes in same pregnancy episode

	gen loss_rank=1 if outcome==7 /*ectopic*/	
	replace loss_rank=2 if outcome==5 /*TOP*/
	replace loss_rank=3 if outcome==4 /*Miscarriage*/
	replace loss_rank=4 if outcome==6 /*Probable TOP*/
	replace loss_rank=5 if outcome==8 /*Molar*/
	replace loss_rank=6 if outcome==10  /*Unspec*/
	replace loss_rank=7 if outcome==9 /*blighted ovum*/
	bysort patid epikey (loss_rank): gen loss_rank_highest=outcome if _n==1
	bysort patid epikey: egen loss_rank_highest_max=max(loss_rank_highest)
	replace outcome=loss_rank_highest_max // n = 7,590

	keep patid epikey loss_date_hes_opcs outcome
	duplicates drop // n = 6,768
	duplicates report patid epikey

	bysort patid epikey (loss_date_hes_opcs): keep if _n==1 // n = 1,079 dropped

	duplicates report patid epikey
	save "$Tempdatadir\hes_opcs_losses.dta", replace
	
********************************************************************************

* Combine pregnancy losses from HES APC and Procedures - need to change names of the outcomes so OPCS doesn't overwrite APC
	
	use "$Tempdatadir\hes_icd_losses.dta", clear
	merge 1:1 patid epikey using "$Tempdatadir\hes_opcs_losses.dta", nogen

* Deal with conflicting loss codes in same pregnancy episode

	gen loss_rank=1 if outcome==7 /*ectopic*/
	replace loss_rank=2 if outcome==5 /*TOP*/
	replace loss_rank=3 if outcome==4 /*Miscarriage*/
	replace loss_rank=4 if outcome==6 /*Probable TOP*/
	replace loss_rank=5 if outcome==8 /*Molar*/
	replace loss_rank=6 if outcome==10  /*Unspec*/
	replace loss_rank=7 if outcome==9 /*blighted ovum*/
	
	bysort patid epikey (loss_rank): gen loss_rank_highest=outcome if _n==1
	bysort patid epikey: egen loss_rank_highest_max=max(loss_rank_highest)
	replace outcome=loss_rank_highest_max	// n = 0 replaced

	drop loss_rank*

* Generate an estimated delivery date for each retained record: taking the earliest of the potential delivery dates (based on antedur or postdur) and procedure dates. For records with no potential delivery dates or procedures, epistart was used.

	gen loss_date=.
	replace loss_date=min(loss_date_hes_opcs, loss_date_hes_apc)
	drop if loss_date==.
	format loss_date %td
	keep patid epikey loss_date outcome

	tab outcome
 
* Create delivery episodes:grouping together records relating to the same delivery (records with an estimated delivery date <8 weeks  (56 days) after the initial record were deemed to relate to the same delivery)
	
	gen toassign = -1
	gen group = 0
	local g = 1
	qui count if toassign

	qui while r(N) > 0 {
		
		bysort patid (toassign loss_date) : replace group = `g' if (loss_date - loss_date[1]) <= 56 & toassign
		replace toassign = 0 if group == `g'
		local ++g
		count if toassign

	}

* Deal with conflicting loss codes in same pregnancy episode

	gen loss_rank=1 if outcome==7 /*ectopic*/
	replace loss_rank=2 if outcome==5 /*TOP*/
	replace loss_rank=3 if outcome==4 /*Miscarriage*/
	replace loss_rank=4 if outcome==6 /*Probable TOP*/
	replace loss_rank=5 if outcome==8 /*Molar*/
	replace loss_rank=6 if outcome==10  /*Unspec*/	
	replace loss_rank=7 if outcome==9 /*blighted ovum*/

	bysort patid group (loss_rank): gen loss_rank_highest=outcome if _n==1
	bysort patid group: egen loss_rank_highest_max=max(loss_rank_highest)
	replace outcome=loss_rank_highest_max // n = 7,277

* Keep first first in delivery episode

	bysort patid group (loss_date): keep if _n==1 

	save "$Tempdatadir\hes_losses_all.dta", replace
	
* Add in pregnancy cohort: keep losses with 140 days of preg start for unknown outcome pregnancies

	use "$Tempdatadir\pregreg_hesoutcomesstep_extra_vars.dta", clear
	bysort patid: gen bign=_N /*unique patids*/
	tab bign /*max 24 pregnancies*/
	sort patid pregstart
	summ bign
	local pregmax = r(max)
	drop big
	
	forvalues x=1/`pregmax' {

		use "$Tempdatadir\pregreg_hesoutcomesstep_extra_vars_`x'", clear
		keep if outcome==13
		keep if linkage_hes==1
		drop outcome

		merge 1:m patid using "$Tempdatadir\hes_losses_all.dta", keep(match)
		gen diff=loss_date-(pregstart_num+28)
		sum diff, d

		if _N > 0 {
	
			hist diff

		}
	
		tab diff if diff>-100 & diff<100
		keep if diff>=0 & diff<=140
		tempfile episodes_all_`x'
		save `episodes_all_`x'' 
	
	}

	use `episodes_all_1', clear
	forvalues x=2/`pregmax' {
	
		append using `episodes_all_`x''

	}

	drop loss_rank*

* Deal with conflicting loss codes in same pregnancy episode
	
	gen loss_rank=1 if outcome==7 /*ectopic*/
	replace loss_rank=2 if outcome==5 /*TOP*/
	replace loss_rank=3 if outcome==4 /*Miscarriage*/
	replace loss_rank=4 if outcome==6 /*Probable TOP*/
	replace loss_rank=5 if outcome==8 /*Molar*/
	replace loss_rank=6 if outcome==10  /*Unspec*/
	replace loss_rank=7 if outcome==9 /*blighted ovum*/
	bysort patid group (loss_rank): gen loss_rank_highest=outcome if _n==1
	bysort patid group: egen loss_rank_highest_max=max(loss_rank_highest)
	replace outcome=loss_rank_highest_max

* Keep first loss record in episode

	bysort patid pregid (loss_date): keep if _n==1 
	codebook pregid /*21,583*/

* Recalculate pregnancy start dates
	* 9 wks for ectopic and 12 wks for other losses
	
	replace pregstart_num =.
	replace pregend_num =.
	
	replace startsource = 9 if pregstart_num==. // loss_date minus impute numbers
	replace endsource = 8 if pregend_num==. // loss_date
	
	replace pregend_num = loss_date
	
	replace pregstart_num=pregend_num-63 if outcome==7
	replace pregstart_num=pregend_num-84 if outcome!=7
	
	replace gestdays = pregend_num - pregstart_num
	
	br pregstart_num pregend_num gestdays outcome
	
	keep patid pregid pregstart_num pregend_num outcome startsource endsource
	save "$Datadir\derived_data\hes_losses_final_unknowns.dta", replace
	
********************************************************************************
	
* Add in pregnancy cohort: keep losses with 140 days of preg start for unspecified loss pregnancies

	use "$Tempdatadir\pregreg_hesoutcomesstep_extra_vars.dta", clear
	bysort patid: gen bign=_N /*unique patids*/
	tab bign /*max 24 pregnancies*/
	sort patid pregstart
	summ bign
	local pregmax = r(max)
	drop big
	
	forvalues x=1/`pregmax' {

		use "$Tempdatadir\pregreg_hesoutcomesstep_extra_vars_`x'", clear
		keep if outcome==10
		keep if linkage_hes==1
		drop outcome

		merge 1:m patid using "$Tempdatadir\hes_losses_all.dta", keep(match)
		gen diff=loss_date-(pregstart_num+28)
		sum diff, d

		if _N > 0 {
	
			hist diff

		}
	
		tab diff if diff>-100 & diff<100
		keep if diff>=0 & diff<=140
		tempfile episodes_all_`x'
		save `episodes_all_`x'' 
	
	}

	use `episodes_all_1', clear
	forvalues x=2/`pregmax' {
	
		append using `episodes_all_`x''

	}

	drop loss_rank*

* Deal with conflicting loss codes in same pregnancy episode
	
	gen loss_rank=1 if outcome==7 /*ectopic*/
	replace loss_rank=2 if outcome==5 /*TOP*/
	replace loss_rank=3 if outcome==4 /*Miscarriage*/
	replace loss_rank=4 if outcome==6 /*Probable TOP*/
	replace loss_rank=5 if outcome==8 /*Molar*/
	replace loss_rank=6 if outcome==10  /*Unspec*/
	replace loss_rank=7 if outcome==9 /*blighted ovum*/
	bysort patid group (loss_rank): gen loss_rank_highest=outcome if _n==1
	bysort patid group: egen loss_rank_highest_max=max(loss_rank_highest)
	replace outcome=loss_rank_highest_max

* Keep first loss record in episode

	bysort patid pregid (loss_date): keep if _n==1 
	codebook pregid /*9,991*/
	
	tab outcome

* Recalculate pregnancy start dates
	* 9 wks for ectopic and 12 wks for other losses
	
	tab startsource
	
	/*replace pregstart_num =.
	replace pregend_num =.
	
	gen startsource = 9 if pregstart_num==. // loss_date minus impute numbers
	gen endsource = 8 if pregend_num==. // loss_date
	
	replace pregstart_num=loss_date-63 if outcome==7 & pregstart_num==.
	replace pregstart_num=loss_date-84 if outcome!=7 & pregstart_num==.

	replace pregend_num = loss_date
	
	gen gestdays_new = pregend_num - pregstart_num*/
	
	br pregstart_num pregend_num gestdays startsource outcome 
	
	keep patid pregid pregstart_num pregend_num outcome startsource endsource
	save "$Datadir\derived_data\hes_losses_final_unspec.dta", replace
	
********************************************************************************

* Add in pregnancy cohort: keep losses with 140 days of preg start for probable top pregnancies

	use "$Tempdatadir\pregreg_hesoutcomesstep_extra_vars.dta", clear
	bysort patid: gen bign=_N /*unique patids*/
	tab bign /*max 24 pregnancies*/
	sort patid pregstart
	summ bign
	local pregmax = r(max)
	drop big
	
	forvalues x=1/`pregmax' {

		use "$Tempdatadir\pregreg_hesoutcomesstep_extra_vars_`x'", clear
		keep if outcome==6
		keep if linkage_hes==1
		drop outcome

		merge 1:m patid using "$Tempdatadir\hes_losses_all.dta", keep(match)
		gen diff=loss_date-(pregstart_num+28)
		sum diff, d

		if _N > 0 {
	
			hist diff

		}
	
		tab diff if diff>-100 & diff<100
		keep if diff>=0 & diff<=140
		tempfile episodes_all_`x'
		save `episodes_all_`x'' 
	
	}

	use `episodes_all_1', clear
	forvalues x=2/`pregmax' {
	
		append using `episodes_all_`x''

	}

	drop loss_rank*

* Deal with conflicting loss codes in same pregnancy episode
	
	gen loss_rank=1 if outcome==7 /*ectopic*/
	replace loss_rank=2 if outcome==5 /*TOP*/
	replace loss_rank=3 if outcome==4 /*Miscarriage*/
	replace loss_rank=4 if outcome==6 /*Probable TOP*/
	replace loss_rank=5 if outcome==8 /*Molar*/
	replace loss_rank=6 if outcome==10  /*Unspec*/
	replace loss_rank=7 if outcome==9 /*blighted ovum*/
	bysort patid group (loss_rank): gen loss_rank_highest=outcome if _n==1
	bysort patid group: egen loss_rank_highest_max=max(loss_rank_highest)
	replace outcome=loss_rank_highest_max

* Keep first loss record in episode

	bysort patid pregid (loss_date): keep if _n==1 
	codebook pregid /*51,461*/
	
	tab outcome

* Recalculate pregnancy start dates
	* 9 wks for ectopic and 12 wks for other losses
	
	tab startsource
	
	/*replace pregstart_num =. 
	replace pregend_num =.
	
	gen startsource = 9 if pregstart_num==. // loss_date minus imputed values
	gen endsource = 8 if pregend_num==. // loss_date
	
	replace pregstart_num=loss_date-63 if outcome==7 & pregstart_num==.
	replace pregstart_num=loss_date-84 if outcome!=7 & pregstart_num==.

	replace pregend_num = loss_date
	
	gen gestdays_new = pregend_num - pregstart_num*/
	
	br pregstart_num pregend_num gestdays startsource outcome
	
	keep patid pregid pregstart_num pregend_num outcome startsource endsource
	save "$Datadir\derived_data\hes_losses_final_prob_tops.dta", replace
	
********************************************************************************
