********************************************************************************

* Merge conflicting episodes in the pregnancy register that are consistent with problem 4 in Jenny Campbell's BMJ paper on uncertain pregnancies

* Author: Paul Madley-Dowd (adapted for antidepressant project by Flo Martin)

* Date: 04/08/2022

********************************************************************************

* Datasets generated by this do-file

	* Conflict resolutions generated by JC's algorithm pregconflicts_Scen4.dta

********************************************************************************

* Prepare the dataset for conflicting episodes - one row per conflicting pregnancy

	use "$Datadir\formatted_linked_data\pregnancyregister.dta", replace
	merge 1:1 patid pregid using "$Tempdatadir\pregreg_preconflictstep.dta", update replace keep(1 3 4 5) keepusing(pregstart_num pregend_num outcome) 
	
	order patid pregid pregstart_num pregend_num
	drop _merge
	
	tab conflict // n = 418,061 conflicting pregnancies
	keep if conflict==1
	sort patid pregstart_num pregend_num
	
	foreach var in pregid pregstart_num pregend_num outcome endadj firstantenatal_num gestdays {
		
		tostring `var', gen(str_`var') force format(%13.0g)
		gen p1_`var' = str_`var'[_n-1]
		destring p1_`var', replace
		rename `var' p2_`var'
		
	}

	format %d p1_pregstart_num p1_pregend_num p1_firstantenatal
	format %13.0g p1_pregid
	duplicates tag p1_pregid, gen(_dupp1pregid)
	drop if _dupp1pregid>0 // 245,079 dropped
	assert _dupp1pregid ==0 
	
	by patid: keep if _n > 1 // Keep all rows after the first one for each mother ID (data now stored on subsequent row)
	keep if (p1_pregstart_num <= p2_pregstart_num & p2_pregstart_num <= p1_pregend_num) // Keeping overlapping pregnancies (as previously sorted on pregstart, pregstart of the second pregnancy must fall within the window of the pregnancy start and end of the first pregnancy)
	
	recode p1_outcome (4/10 = 1) (1/3 11 12 = 2) (13 = 3), gen(p1_outcome_rc)
	recode p2_outcome (4/10 = 1) (1/3 11 12 = 2) (13 = 3), gen(p2_outcome_rc)
	label define lb_outrc 1"Loss" 2"Delivery" 3"Missing"
	label values p1_outcome_rc p2_outcome_rc lb_outrc
	
	tab p1_outcome_rc
	tab p2_outcome_rc
	
	gen outcome_combo = 1 if p1_outcome_rc==1 & p2_outcome_rc==1 // Generate combination variable for outcomes (earliest pregnancy - labelled as prev - comes first) using appendix 12 of BMJ paper by Jenny Campbell
	replace outcome_combo = 2 if p1_outcome_rc==1 & p2_outcome_rc==2
	replace outcome_combo = 3 if p1_outcome_rc==1 & p2_outcome_rc==3
	replace outcome_combo = 4 if p1_outcome_rc==2 & p2_outcome_rc==2
	replace outcome_combo = 5 if p1_outcome_rc==2 & p2_outcome_rc==3
	replace outcome_combo = 6 if p1_outcome_rc==3 & p2_outcome_rc==3
	
	label define lb_outcombo 1"Loss - loss" 2"Loss - delivery" 3"Loss - unknown" 4"Delivery - delivery" 5"Delivery - unknown" 6"Unknown - unknown"
	label values outcome_combo lb_outcombo
	
	keep patid p1* p2* outcome_combo pregnumber
	order patid p1* p2*
	
	save "$Tempdatadir\pregconflicts.dta", replace
	
********************************************************************************

* Resolve scenarios from Jenny Campbell's BMJ paper

* Notes from JC - for pairs consistent with 4a, 4b and 4c I would keep the first episode and drop the second. 4d and 4e I would consider merging the episodes - 4d is the biggest headache as these are essentially sporadic antenatal codes in the data being made into multiple episodes. I would suggest taking the start of pregnancy from the episode which has a startsource derived from the data. You could then treat this as a pregnancy with no outcome as above.

********************************************************************************

/* Scenario 4a - The GP records further information about a pregnancy outcome >25 weeks later for deliveries or >8-12 weeks for losses

	- 1	The outcome combination of the two episodes must be delivery/delivery or loss/loss (Appendix 12)
	- 2	The first episode had an antenatal code from a list deemed likely to only be recorded if the patient was currently pregnant (codelist READ_currently_pregnant_codes_JCAMPBELL) OR a scan recorded in the HES DID data between firstantenatal* and pregend
	
	* NOTE - we do not have access to HES Diagnostic Imaging (DID) so can only implement point 1
	
*/

* Identify all conflicting pairs where the first pregnancy has evidence for being a current pregnancy

	use "$Tempdatadir\pregconflicts.dta", clear
	keep if inlist(outcome_combo, 1, 4)
	tab outcome_combo
	
	by patid: egen _seq = seq()
	summ _seq // n = 32,891
	local max = r(max)
	
	forvalues i = 1/`max' {
		
		disp `i'
		preserve
		
		keep if _seq==`i'
		merge 1:m patid using "$Tempdatadir\pregreg_conflicts_currentlypregnantevents.dta", nogen keep(3)
		keep if p1_pregstart_num <= eventdate_num & eventdate_num <= p1_pregend_num
		
		keep patid p1_pregid p2_pregid
		gen EvScen_4a = 1
		
		if _N > 0 {
			
			duplicates drop
			
		}
		
		save "$Tempdatadir\EvScen4a_`i'.dta", replace
		
		restore
		
	}
	
* Append datasets together

	use "$Tempdatadir\EvScen4a_1.dta", clear
	forvalues i = 2/`max' {
		
		append using "$Tempdatadir\EvScen4a_`i'.dta"
		
	}
	
	save "$Tempdatadir\EvScen4a.dta", replace
	
********************************************************************************
	
/* Scenario 4b - The GP records further antenatal information after the end of pregnancy. Conflicting episodes are generated by the algorithm

	- 1 The first episode must have an outcome = 1 - 10 in the register (Appendix 2) and must have endadj* = 0 (need to get this from CPRD 29/06)
	- 2 The second episode must have no recorded outcome i.e. outcome = 13
	- 3 The second episode must have a gestdays* = 28 (likely to consist of one code) and there must not be a scan code (codelist READ_antenatal_scan_JCAMPBELL) with an eventdate* = pregend* of the second episode
	
*/

	use "$Tempdatadir\pregconflicts.dta", clear
	keep if inlist(p1_outcome,1,2,3,4,5,6,7,8,9,10) & p1_endadj==0 & p2_outcome==13 & p2_gestdays==28
	
	by patid: egen _seq = seq()
	summ _seq
	local max = r(max)
	
	forvalues i = 1/`max' {
		
		disp `i'
		preserve
		
		keep if _seq == `i'
		merge 1:m patid using "$Tempdatadir\pregreg_conflicts_antenatalscanevents.dta", keep(1 3)
		
		gen scan_test = 1 if _merge==3 & eventdate_num == p2_pregend_num
		replace scan_test = 0 if _merge==1 | (_merge==3 & eventdate_num != p2_pregend_num)
		
		bysort patid: egen _seq2 = seq()
		
		keep patid p1_pregid p2_pregid scan_test _seq2
		reshape wide scan_test, i(patid p1 p2) j(_seq2)
		egen scan_test = rowmax(scan_test*)
		
		drop if scan_test == 1 
		keep patid p1_pregid p2_pregid
		gen EvScen_4b = 1
		
		save "$Tempdatadir\EvScen4b_`i'.dta", replace
		
		restore
		
	}
	
* Append datasets together

	use "$Tempdatadir\EvScen4b_1.dta", clear
	
	forvalues i = 2/`max' {
		
		append using "$Tempdatadir\EvScen4b_`i'.dta"
		
	}
	
	save "$Tempdatadir\EvScen4b.dta", replace
	
********************************************************************************

/* Scenario 4c - The patient has a follow up scan after a pregnancy loss. The scan is recorded in the data as an antenatal scan, a conflicting episode is then generated by the algorithm.

	- 1 The outcome combination of the two episodes must be loss - missing
	- 2 The second episode must have a gestdays* = 28 (likely to consist of one code) and there must be a scan code (codelist READ_antenatal_scan_JCAMPBELL) with an eventdate* = pregend* of the second episode
	
*/

	use "$Tempdatadir\pregconflicts.dta", clear
	keep if outcome_combo == 3 & p2_gestdays == 28
	
	if _N>0 {
		
		by patid: egen _seq = seq()
		summ _seq
		local max = r(max)
		
		forvalues i = 1/`max' {
			
			disp `i'
			preserve
			
			keep if _seq == `i'
			merge 1:m patid using "$Tempdatadir\pregreg_conflicts_antenatalscanevents.dta", keep(3)
			
			keep if eventdate_num == p2_pregend_num
			keep patid p1_pregid p2_pregid
			gen EvScen_4c = 1
			
			if _N>0 {
				
				duplicates drop
				
			}
			
			save "$Tempdatadir\EvScen4c_`i'.dta", replace 
			
			restore
			
		}
		
		* Append datasets together
		
		use "$Tempdatadir\EvScen4c_1.dta", clear
		forvalues i = 2/`max' {
			
			append using "$Tempdatadir\EvScen4c_`i'.dta"
			
		}
		
	}
	
	if _N == 0 {
		
		keep patid p1_pregid p2_pregid
		gen EvScen_4c = .
		
	}
	
	save "$Tempdatadir\EvScen4c.dta", replace
	
********************************************************************************

/* Scenario 4d - The GP records information about a pregnancy but no outcome with >6 between records. If the second episode has gestational information, the start may be assigned before the start of the first episode

	- 1 The outcome combination must be missing - missing 
	- 2 The pregend* of the first episode is >42 days before the firstantenatal* date of the second episode
	
*/

	use "$Tempdatadir\pregconflicts.dta", clear
	keep if outcome_combo == 6 & (p1_pregend_num + 42 < p2_firstantenatal)
	gen EvScen_4d = 1
	save "$Tempdatadir\EvScen4d.dta", replace
	
********************************************************************************

/* Scenario 4e - The pregnancy dates have been shifted backwards by the rules of the algorithm leaving uncovered records. Conflicting episodes are generated by the algorithm

	- 1 The first episode must have a delivery outcome code and endadj* != 0 (need to obtain from CPRD 29/06)
	- 2 The second episode must have an outcome = 11, 12 or 13
	
*/

	use "$Tempdatadir\pregconflicts.dta", clear
	keep if (p1_outcome_rc == 2 & p1_endadj != 0) & inlist(p2_outcome, 11, 12, 13)
	gen EvScen_4e = 1
	save "$Tempdatadir\EvScen4e.dta", replace
	
********************************************************************************

* Merge the evidence of each scenario onto list of conflicting pregnancies

	use "$Tempdatadir\pregconflicts.dta", clear
	merge 1:1 patid p1_pregid p2_pregid using "$Tempdatadir\EvScen4a.dta", nogen
	merge 1:1 patid p1_pregid p2_pregid using "$Tempdatadir\EvScen4b.dta", nogen
	merge 1:1 patid p1_pregid p2_pregid using "$Tempdatadir\EvScen4c.dta", nogen
	merge 1:1 patid p1_pregid p2_pregid using "$Tempdatadir\EvScen4d.dta", nogen
	merge 1:1 patid p1_pregid p2_pregid using "$Tempdatadir\EvScen4e.dta", nogen
	
	keep patid *pregid EvScen* pregnumber
	egen conflict_retain = rowmax(EvScen*)
	tab conflict_retain, m
	keep if conflict_retain==1
	drop conflict_retain
	
	tab EvScen_4a
	tab EvScen_4b
	tab EvScen_4c
	tab EvScen_4d
	tab EvScen_4e
	
	save "$Datadir\derived_data\pregconflicts_Scen4.dta", replace

********************************************************************************
