********************************************************************************

* Selecting eligible patients from all data sent by CPRD - running the conflicting & historical pregnancies algorithm stuff first, then deriving parity and gravidity, then making other exclusions

* Author: Paul Madley-Dowd (adapted by Flo Martin from PREPArE scripts)

* Date: 03/11/2022

********************************************************************************

* Datasets generated by this do-file

* 	- $Datadir\derived_data\preg_reg_reduced.dta
*		- $Datadir\derived_data\pregnancy_cohort_final.dta
*		- $Datadir\derived_data\pregnancy_cohort_matids_pregids.dta

********************************************************************************

* Start logging

	log using "$Logdir\2_eligibility\1_population eligibility.txt", replace

********************************************************************************

	* Load in the pregnancy register

	use "$Datadir\formatted_linked_data\pregnancyregister.dta", clear
	order patid pregid pregstart_num pregend_num conflict outcome pregnumber
	keep patid pregid pregstart_num pregend_num conflict outcome pregnumber gestdays babypatid startsource endsource
	
	count // n = 7,526,049 pregnancies
	codebook patid // among 3,163,066 patients

	save "$Datadir\derived_data\preg_reg_reduced.dta", replace 

* Join patient data with list of maternal IDs and pregnancy register information	

	use "$Datadir\formatted_cprd_data\All_Patient.dta", clear
	merge 1:m patid using "$Datadir\derived_data\preg_reg_reduced.dta" 
	tab _merge 
	
	/* n = 1,172,137 in the Patient without a pregnancy (babies of women in Pregnancy Register) and 0 in the Preg Reg without patient data (excluded on the basis on consent withdrawal/new patients in Sept build) */
	
	keep if _merge==3
	drop _merge
	
	count // n = 7,526,049 pregnancies...
	codebook patid // ...among 3,163,066 patients
	
	save  "$Tempdatadir\pregreg_hesoutcomesstep.dta", replace  // dataset used for merging HES outcomes together in 4_cleaning hes data.do
	
********************************************************************************

* Do file that cleans the HES data and distinguishes miscarriage/termination outcomes

	do "$Dodir\2_cleaning\2a_cleaning hes data.do"
	
********************************************************************************

* Merge in linked HES data for pregnancies that have unknown outcomes

	use "$Tempdatadir\pregreg_hesoutcomesstep.dta", replace
	cap drop _m
	
	tab outcome
	lab define outcome 1 "Live birth" 2 "Stillbirth" 3 "Live birth or still birth" ///
4 "Miscarriage" 5 "TOP" 6 "Probable TOP" 7"Ectopic" 8 "Molar" 9 "Blighted ovum" 10 "Unspecified loss" 11 "Delivery based on a third trimester pregnancy record" 12 "Delivery based on a late pregnancy record"  13 "Outcome unknown"
	lab val outcome outcome
	tab outcome
	
/*               
              Outcome of pregnancy (see |
                         documentation) |      Freq.     Percent        Cum.
----------------------------------------+-----------------------------------
                             Live birth |  4,427,215       58.83       58.83
                             Stillbirth |     27,916        0.37       59.20
              Live birth or still birth |        431        0.01       59.20
                            Miscarriage |    686,919        9.13       68.33
                                    TOP |    174,536        2.32       70.65
                           Probable TOP |    802,676       10.67       81.31
                                Ectopic |     62,935        0.84       82.15
                                  Molar |      5,870        0.08       82.23
                          Blighted ovum |      4,422        0.06       82.29
                       Unspecified loss |    114,228        1.52       83.80
Delivery based on a third trimester pre |     47,026        0.62       84.43
Delivery based on a late pregnancy reco |     27,001        0.36       84.79
                        Outcome unknown |  1,144,874       15.21      100.00
----------------------------------------+-----------------------------------
                                  Total |  7,526,049      100.00
*/
	
	tab conflict // n = 559,237
	
	* Merge in the dataset where outcomes where obtained from HES (losses) for unknown outcomes in the Pregnancy Register
	
	rename gestdays gestdays_old
	rename startsource startsource_orig
	rename endsource endsource_orig
	
	merge 1:1 patid pregid using "$Datadir\derived_data\hes_losses_final_unknowns.dta", update replace keep(1 3 4 5)
	
		// 21,583 updated 
	
	keep patid pregid pregstart_num pregend_num outcome gestdays conflict startsource* endsource* _merge
	
	tab outcome
	
	* Outcome unknown   1,123,311 - 14.93%
	
	tab startsource
	tab startsource_orig, nol
	
	replace startsource_orig = startsource if startsource!=.
	tab startsource_orig
	 
	replace endsource_orig = endsource if endsource!=.
	tab endsource_orig

	drop startsource endsource
	
	gen outcome_updated_flag = 0 if _merge==1
	replace outcome_updated_flag = 1 if _merge==5
	
	drop _merge
	
	* Merge in the dataset where outcomes where obtained from HES (losses) for unspecified losses in the Pregnancy Register
	
	merge 1:1 patid pregid using "$Datadir\derived_data\hes_losses_final_unspec.dta", update replace keep(1 3 4 5)
	
		// 9,171 updated
	
	tab outcome
	
	* Unspecified losses  105,519 - 1.40%
	
	replace startsource_orig = startsource if startsource!=.
	tab startsource_orig
	 
	replace endsource_orig = endsource if endsource!=.
	tab endsource_orig

	drop startsource endsource
	
	replace outcome_updated_flag = 3 if _merge==5
	
	drop _merge
	
	* Merge in the dataset where outcomes where obtained from HES (losses) for probable TOPs in the Pregnancy Register
	
	merge 1:1 patid pregid using "$Datadir\derived_data\hes_losses_final_prob_tops.dta", update replace keep(1 3 4 5)
	
		// 51,365 updated
	
	tab outcome
	
	* Probable TOPs 751,311 - 9.98%
	
	replace startsource_orig = startsource if startsource!=.
	tab startsource_orig
	 
	replace endsource_orig = endsource if endsource!=.
	tab endsource_orig

	drop startsource endsource
	
	replace outcome_updated_flag = 2 if _merge==5
	
	drop _merge
	
	tab outcome
	
	codebook patid // n = 3,163,066 
	codebook pregid // n = 7,526,049
	
	* Merge in the dataset where outcomes where obtained from HES for outcomes that were based on a third trimester pregnancy in the Pregnancy Register
	
	merge 1:1 patid pregid using "$Datadir\derived_data\hes_deliveries_final_t3_record.dta", update replace keep(1 3 4 5)
	
		// 20,253 updated
	
	tab outcome
	
	* T3 record 26,773 - 0.36%
	
	replace outcome_updated_flag = 1 if _merge==5
	
	drop _merge
	
	* Merge in the dataset where outcomes where obtained from HES for outcomes that were based on a late pregnancy outcome in the Pregnancy Register
	
	merge 1:1 patid pregid using "$Datadir\derived_data\hes_deliveries_final_late_preg.dta", update replace keep(1 3 4 5)
	
		// 12,849 updated
	
	tab outcome
	
	* Late delivery record 14,152 - 0.19%
	
	replace outcome_updated_flag = 1 if _merge==5
	
	tab outcome

	drop _merge
	
	* Merge in the dataset where outcomes where obtained from HES for unknown outcomes in the Pregnancy Register
	
	merge 1:1 patid pregid using "$Datadir\derived_data\hes_deliveries_final_unknowns.dta", update replace keep(1 3 4 5)
	
		// 182,377 updated
	
	tab outcome
	
	* Unknown outcome 941,291 - 12.51%
	
	replace startsource_orig = startsource if startsource!=.
	tab startsource_orig
	 
	replace endsource_orig = endsource if endsource!=.
	tab endsource_orig

	drop startsource endsource
	
	replace outcome_updated_flag = 1 if _merge==5
	
	tab outcome
	drop _merge 
	
* Start and end source

	merge 1:1 patid pregid using "$Datadir\formatted_linked_data\pregnancyregister.dta", update replace keep(3 4) keepusing(babypatid) 
	
	rename startsource_orig startsource
	rename endsource_orig endsource

	tab startsource
	tab endsource
	
	label variable outcome_updated_flag "Pregnancy outcome updated"
	label define lb_outcome_updated 0"Outcome not updated" 1"Updated using deliveries" 2"Updated using losses" 3"Updated unspecified losses"
	label values outcome_updated_flag lb_outcome_updated
	tab outcome_updated_flag if outcome_updated_flag!=0
	count if outcome_updated_flag>0 // n = 297,238 pregnancies updated using HES
	
	/*drop gestdays_new
	gen gestdays_new = gestwks*7
	tab gestdays_new*/
	
	replace gestdays=gestdays_old if gestdays==.
	gen flag = 1 if gestdays!=gestdays_old
	count if flag==1
	
	tab outcome if flag==1
	tab startsource if flag==1
	/*replace startsource = 10 if flag==1 & startsource!=7
	replace pregstart_num = pregend_num - gestdays_new if gestdays_new!=.
	
	label define startsource_lb 7"Delivery date in HES minus gestat" 8"Delivery date in HES minus 280 days (full term) or 252 (preterm)" 9"Loss date minus 84 days (early loss) or 63 days (ectopic)" 
	label values startsource startsource_lb
	
	tab startsource
	
	gen gest_length = pregend_num - pregstart_num
	sum gest_length*/
	
	save "$Tempdatadir\pregreg_preconflictstep.dta", replace

********************************************************************************

	* Run algorithm to resolve conflicting pregnancies (overlap in days between pregnancies among same mum)

	use "$Tempdatadir\pregreg_preconflictstep.dta", clear

	preserve
		keep if conflict==1
		codebook patid // n = 247,731 patients with conflicting pregnancies
		codebook pregid // n = 559,237 conflicting pregnancies
	restore
	
* Create list of all events showing evidence for a current pregnancy and antenatal scans (needed for conflicts and historical pregnancies algorithms)

	forvalues x=0/11 {

		use "$Datadir\formatted_cprd_data\Clinical_`x'.dta", replace
		
	* Create a list of all the events showing evidence for a current pregnancy

		preserve
		merge m:1 medcode using "$Codesdir\READ_currently_pregnant_codes_JCAMPBELL.dta", nogen keep(3)
		save "$Tempdatadir\pregreg_conflicts_currentlypregnantevents_`x'.dta", replace
		
	* Create a list of all events showing evidence of an antenatal scan

		restore
		merge m:1 medcode using "$Codesdir\READ_antenatal_scan_JCAMPBELL.dta", nogen keep(3)
		save "$Tempdatadir\pregreg_conflicts_antenatalscanevents_`x'.dta", replace
		
	}
	
	use "$Datadir\formatted_cprd_data\All_Referral.dta", replace
		
	* Create a list of all the events showing evidence for a current pregnancy

		preserve
		merge m:1 medcode using "$Codesdir\READ_currently_pregnant_codes_JCAMPBELL.dta", nogen keep(3)
		save "$Tempdatadir\pregreg_conflicts_currentlypregnantevents_ref.dta", replace
		
	* Create a list of all events showing evidence of an antenatal scan

		restore
		merge m:1 medcode using "$Codesdir\READ_antenatal_scan_JCAMPBELL.dta", nogen keep(3)
		save "$Tempdatadir\pregreg_conflicts_antenatalscanevents_ref.dta", replace
		
* Append together the currently pregnant events from all referral and all clinical
	
	use "$Tempdatadir\pregreg_conflicts_currentlypregnantevents_ref.dta", clear
	
	forvalues x=0/11 {
		
		append using "$Tempdatadir\pregreg_conflicts_currentlypregnantevents_`x'.dta"
		
	}
	
	save "$Tempdatadir\pregreg_conflicts_currentlypregnantevents.dta", replace
	
* Append together the antenatal scan events from all referral and all clinical	
	
	use "$Tempdatadir\pregreg_conflicts_antenatalscanevents_ref.dta", clear
	
	forvalues x=0/11 {
		
		append using "$Tempdatadir\pregreg_conflicts_antenatalscanevents_`x'.dta"
		
	}
	
	save "$Tempdatadir\pregreg_conflicts_antenatalscanevents.dta", replace
	
* Erase temporary datasets created here
	
	erase "$Tempdatadir\pregreg_conflicts_antenatalscanevents_ref.dta"
	erase "$Tempdatadir\pregreg_conflicts_currentlypregnantevents_ref.dta"
	
	forvalues x=0/11 {
		
		erase "$Tempdatadir\pregreg_conflicts_currentlypregnantevents_`x'.dta"
		erase "$Tempdatadir\pregreg_conflicts_antenatalscanevents_`x'.dta"
		
	}
	
********************************************************************************

* Run the pregnancy conflicts algorithm - Jenny Campbell

	do "$Dodir\2_eligibility\2b_pregreg conflicts.do"
	
********************************************************************************

* Load in data at the start of the step

	use "$Tempdatadir\pregreg_preconflictstep.dta", clear 
	
* Merge on data from conflicts algorithm and determine which pregnancies to keep 

	rename pregid p1_pregid 
	merge 1:1 patid p1_pregid using "$Datadir\derived_data\pregconflicts_Scen4.dta", keepusing(patid p1_pregid Ev* pregnumber) gen(_conflict1_p1) 
	rename Ev* p1_Ev*
	rename p1_pregid p2_pregid
	merge 1:1 patid p2_pregid using "$Datadir\derived_data\pregconflicts_Scen4.dta", keepusing(patid p2_pregid Ev* pregnumber) gen(_conflict1_p2) 
	rename p2_pregid pregid
	rename Ev* p2_Ev*
	
	gen conflict_retain = 99 if conflict==0
	label variable conflict_retain ""
	label define lb_conflict_retain -1"Conflict to be dropped" 1"Conflict retained in the first step" 2"Conflict retained in the second step (historical pregnancies)" 99"No conflict"
	label values conflict_retain lb_conflict_retain
	
	replace conflict_retain = 1 if _conflict1_p1==3 & (p1_EvScen_4a==1 | p1_EvScen_4b==1 | p1_EvScen_4c==1) // records to be kept = 45,907
	replace conflict_retain = -1 if _conflict1_p2==3 & (p2_EvScen_4a==1 | p2_EvScen_4b==1 | p2_EvScen_4c==1) // records to be dropped = 45,907
	
	count if p1_EvScen_4a == 1 // n = 30,242 pregnancies fitting scenario 4a
	count if p1_EvScen_4b == 1 // n = 15,470 pregnancies fitting scenario 4b
	count if p1_EvScen_4c == 1 // n = 195 pregnancies fitting scenario 4c
	count if p1_EvScen_4d == 1 // n = 1,010 pregnancies fitting scenario 4d
	count if p1_EvScen_4e == 1 // n = 14,901 pregnancies fitting scenario 4e
	tab conflict_retain, mis
	
	* n = 45,907 pregnancies to be dropped 
	* n = 44,876 pregnancies retained using the algorithm
	
	count if (_conflict1_p1==3 & p1_EvScen_4a==1 | p1_EvScen_4b==1 | p1_EvScen_4c==1) & (_conflict1_p2==3 & p2_EvScen_4a==1 | p2_EvScen_4b==1 | p2_EvScen_4c==1) // n = 1,031 conflicts where the first pregnancy is also the second pregnancy of a second conflict (i.e. chain of conflicts) - keeping only the first pregnancy of the chain
	
	drop if conflict_retain==-1 // n = 45,924 pregnancies dropped after the conflict algorithm implemented
	
	save "$Tempdatadir\pregreg_prehistoricalpregstep.dta", replace
	
********************************************************************************

* Run algorithm to exclude episodes which are likely to be derived from historical data

	do "$Dodir\2_eligibility\2c_pregreg_historical_episodes_exclude.do"
	
********************************************************************************

* Load in the pre-historical pregnancy data

	use "$Tempdatadir\pregreg_prehistoricalpregstep.dta", clear
	
* Implement conflicts section of historical data uncertain pregnancies

	rename pregid p1_pregid
	merge 1:1 patid p1_pregid using "$Datadir\derived_data\Hist_Conflict.dta", keepusing(patid p1_pregid Hist_Conflict*) gen(_conflict2_p1) keep(1 3)
	rename Hist_Conflict* p1_Hist_Conflict*
	rename p1_pregid p2_pregid
	merge 1:1 patid p2_pregid using "$Datadir\derived_data\Hist_Conflict.dta", keepusing(patid p2_pregid Hist_Conflict*) gen(_conflict2_p2) keep(1 3)
	rename p2_pregid pregid
	rename Hist_Conflict* p2_Hist_Conflict*
	
* Drop first episode, retain second episode for conflicts

	tab conflict_retain, mis
	replace conflict_retain = 2 if _conflict2_p2==3 & (p2_Hist_Conflict_1a==1 | p2_Hist_Conflict_1b==1) & conflict_retain==. // 9,024 records to be kept
	replace conflict_retain = -1 if _conflict2_p1==3 & (p1_Hist_Conflict_1a==1 | p1_Hist_Conflict_1b==1) // 10,199 records to be dropped
	
	tab conflict_retain, mis
	
	count if inlist(conflict_retain, 1, 2) // 53,834 total number of conflicts retained

	count if (_conflict1_p1==3 & (p1_EvScen_4a==1 | p1_EvScen_4b==1 | p1_EvScen_4c==1)) & (_conflict2_p2==3 & (p2_Hist_Conflict_1a==1 | p2_Hist_Conflict_1b==1)) // 976 conflicting pregnancies who were identified to be kept in both steps
	count if (_conflict1_p1==3 & (p1_EvScen_4a==1 | p1_EvScen_4b==1 | p1_EvScen_4c==1)) & (_conflict2_p1==3 & (p1_Hist_Conflict_1a==1 | p1_Hist_Conflict_1b==1)) // 40 conflicting pregnancies identified to be kept in the first step but removed in the second step
	drop if conflict_retain==-1 // 10,195 dropped
	
* Implement unknown outcome section historical data uncertain pregnancies

	* On advice from JC, keep the first pregnancy and dropping the second, retaining a note that the pregnancy end date (of the first pregnancy) is likely to be earlier than the pregend_num value states. Note that the algorithm groups outcome records of the same type (so outcome will be the same broadly between two records) but does not differentiate between termination or miscarriage (so these two could be conflated)
	
	rename pregid p1_pregid
	merge 1:1 patid p1_pregid using "$Datadir\derived_data\Hist_Misout.dta", keepusing(patid p1_pregid Hist_Misout) gen(_misout_p1) keep(1 3)
	rename Hist_Misout p1_Hist_Misout
	rename p1_pregid p2_pregid
	merge 1:1 patid p2_pregid using "$Datadir\derived_data\Hist_Misout.dta", keepusing(patid p2_pregid Hist_Misout) gen(_misout_p2) keep(1 3) 
	rename p2_pregid pregid
	rename Hist_Misout p2_Hist_Misout

	drop if _misout_p2==3 // 144,858 (all that were matched?) with unknown outcome dropped as evidence of a historical pregnancy
	gen ev_pregend_error_Histmisout = 1 if _misout_p1==3 // 144,719 pregnancies retained which may have pregend earlier than it should be
	tab ev_pregend_error_Histmisout
	label variable ev_pregend_error_Histmisout"Evidence pregnancy end date influenced by historical pregnancy"
	
*  Drop remaining conflicts and other issues (including pregnancies lasting less than a day)

	tab conflict_retain conflict, mis
	drop if conflict_retain == . // 429,737 remaining conflicting pregnancies dropped as not picked up in algorithm

* Check for duplicate pregnancies where two pregnancy records (with different pregnancy IDs) start on the same date for a mother 
	
	duplicates report patid pregstart_num // duplicate pregnancies in register (with different pregnancy ID) 
	duplicates tag patid pregstart_num, gen(_check1)
	tab _check1

* Keep all those with a babypatid and then keep the latest pregnancy end date for remaining entries as this is likely the most up to date info
	
	gsort + patid pregstart_num - babypatid pregend_num
	egen _seq = seq(), by(patid pregstart_num)
	drop if _seq >1 // 42,884 pregnancies removed for duplicate pregnancy start date
	drop _seq _check1

* drop pregnancies where pregnancy start = pregnancy end 
	
	drop if pregstart_num == pregend_num // 831 pregnancies dropped
	
********************************************************************************

* Generate a new pregnancy number having dropped conflicting or historical pregnancies

	sort patid pregstart_num
	bysort patid: egen pregnum_new=seq()
	
	rename outcome updated_outcome
	tab updated_outcome
	
/*       
              Outcome of pregnancy (see |
                         documentation) |      Freq.     Percent        Cum.
----------------------------------------+-----------------------------------
                             Live birth |  4,333,050       63.24       63.24
                             Stillbirth |     26,827        0.39       63.63
              Live birth or still birth |      3,171        0.05       63.68
                            Miscarriage |    627,623        9.16       72.84
                                    TOP |    223,577        3.26       76.10
                           Probable TOP |    730,658       10.66       86.77
                                Ectopic |     59,935        0.87       87.64
                                  Molar |      5,442        0.08       87.72
                          Blighted ovum |      4,803        0.07       87.79
                       Unspecified loss |     92,526        1.35       89.14
Delivery based on a third trimester pre |     17,384        0.25       89.39
Delivery based on a late pregnancy reco |      6,658        0.10       89.49
                        Outcome unknown |    719,966       10.51      100.00
----------------------------------------+-----------------------------------
                                  Total |  6,851,620      100.00

*/
	
	keep patid pregid babypatid pregstart_num pregend_num updated_outcome outcome_updated_flag gestdays pregnum_new startsource endsource
	
	* Having removed everything that needed to be removed and updated the dates where needed make sure gestational length (gestdays) reflects the news dates
	
	replace gestdays = pregend_num - pregstart_num
	
	save "$Datadir\derived_data\pregreg_conflict_hist_rm.dta", replace
	
********************************************************************************
	
* Now, using the new pregnumber variable we can derive gravidity and parity before the other exclusions are made so we take into consideration all pregnancies (excluded and included in the final cohort)

	do "$Dodir\2_eligibility\2d_gravidity_pre_exc.do"
	do "$Dodir\2_eligibility\2e_parity_pre_exc.do"
	
******************************************************************************** 
	
* Join patient data with list of maternal IDs and pregnancy register information	
	
	use "$Datadir\formatted_linked_data\pregnancyregister.dta", clear
	
	merge m:1 patid using "$Datadir\formatted_cprd_data\All_Patient.dta", nogen
	merge 1:1 patid pregid using "$Datadir\derived_data\pregreg_conflict_hist_rm.dta", update replace
	
	drop if _merge==1
	count // 6,851,620
	drop _merge
	
	tab updated_outcome
	
	codebook pregid // n = 6,851,620 pregnancies in the original cohort obtained during initial extraction having run the conflicting and historical pregnancy algorithm
	codebook patid // n = 3,123,274
	
******************************************************************************** 
	
* Pregnancies that started before 01/01/95 
	
	keep if missing(pregstart_num)==0 
	keep if pregstart_num>date("01January1996", "DMY")  // 3,175,042 pregnancies started before 01/01/1996
	count // 3,676,578 remaining
	
* Pregnancies that started after 2019

	gen pregyear=year(pregstart_num)
	bysort pregyear: tab updated_outcome 
	drop if pregyear>=2019 // 101,359 pregnancies started from 2019 onwards
	
	codebook patid	// n = 1,849,831 individual patients
	codebook pregid	// n = 3,575,219 individual pregnancies

********************************************************************************

* Now to identify women registered with their practice at least 365 days before pregstart and throughout pregnancy

	cap drop _m
	tostring patid, gen(patid_s) format(%12.0f)
	gen pracid = substr(patid_s,-5,5)
	destring pracid, replace
	merge m:1 pracid using "$Datadir\formatted_cprd_data\All_Practice.dta"
	keep if _merge==3
	drop _merge
	count // n = 3,575,219 remaining
	
	sort pracid patid pregid
	order pracid patid pregid pregstart_num crd_num tod_num uts_num
	list pracid patid pregid pregstart_num crd_num tod_num uts_num in 1/20
	
	gen pregstart_num_minus365 = pregstart_num - 365
	gen pregstart_num_minus6months = pregstart_num - (365/2)
	
	format pregstart_num_minus365 %td
	format pregstart_num_minus6months %td
	
	gen timefrom_preg_uts = uts_num - pregstart_num
	gen timefrom_preg_crd = crd_num - pregstart_num
	
	histogram timefrom_preg_uts
	histogram timefrom_preg_crd
	
	count if uts_num>pregstart_num // n = 917,335 excluded for not achieving UTS before pregstart
	count if crd_num>pregstart_num_minus6months // n = 1,788,249 excluded for current registration date less than 365 days before pregstart
	count if tod_num<pregend_num // n = 61,688 excluded for transferring out before pregend
	
	keep if crd_num<=pregstart_num_minus365 & uts_num<=pregstart_num // n = 2,271,333 deleted
	drop if tod_num<=pregend_num // n = 31,726 deleted
	
	tab updated_outcome
	count // n = 1,272,160

********************************************************************************

* Delete pregnancies with missing age data

	merge m:1 patid using "$Datadir\formatted_cprd_data\All_Patient", keep(3) keepusing(yob) nogen
	count
	drop if yob==1900 | yob==1899 // n = 0 deleted
	
********************************************************************************

* Drop pregnancies which start after patients follow-up ends

	gen start_fup_cprd = max(uts_num, crd_num)
	format start_fup_cprd %td
	
	merge m:1 patid using "$Datadir\formatted_linked_data\hes_death.dta", keep(1 3) nogen
	gen deathdate_num = dod_num
	replace deathdate_num = date(deathdate, "DMY") if deathdate_num==.
	format %d deathdate_num
	gen end_fup_CPRD = min(tod_num, lcd_num, deathdate_num)
	format end_fup_CPRD %td
	
	count if pregstart_num >= tod_num // n = 0
	count if pregstart_num >= lcd_num // n = 21
	count if pregstart_num >= dod_num // n = 1
	count if pregstart_num >= date(deathdate, "DMY") // n = 102 with pregnancy start after date of death from CPRD data
	drop if pregstart_num >= end_fup_CPRD // n = 123 dropped
	
	codebook patid // n = 767,499
	codebook pregid // n = 1,272,037

********************************************************************************

* Drop pregnancies with less than 9 months follow-up prior to last collection date

	tab updated_outcome if pregstart_num + 273.9375 >= lcd_num // this only leaves 26,891 pregnancies?
	tab updated_outcome // compared to 412,534 pregnancies with the current exclusions
	
	tab updated_outcome if pregstart_num + 273.9375 <= lcd_num // I think the sign should be less than or equal to because this means I drop those 26,916
	keep if pregstart_num + 273.9375 <= lcd_num
	
	count // n = 1,245,146
	
********************************************************************************

* Save dataset maternal IDs included in pregnancy cohort
	
	preserve
	
		duplicates drop patid pregid, force // 0
		keep patid pregid babypatid pregstart_num pregend_num updated_outcome outcome_updated_flag pregnum_new startsource endsource mblbabies
		compress
		count // 1,245,146
		save  "$Datadir\derived_data\pregnancy_cohort_conflicts_outcome_update.dta", replace
	
********************************************************************************

* Dataset for use in covariate derivation

	 use "$Datadir\derived_data\pregnancy_cohort_conflicts_outcome_update.dta", clear
	 merge 1:1 patid pregid using "$Datadir\formatted_linked_data\pregnancyregister.dta", keep(match) keepusing(preterm_ev multiple_ev postterm_ev gestdays secondtrim_num thirdtrim_num) nogen
	 merge m:1 patid using "$Datadir\formatted_linked_data\linkage_eligibility.dta", keepusing(hes_apc_e) keep(match) nogen
	
	 recode hes_apc_e .=0
	 tab hes_apc_e // 46% with linked APC data
	 
	 * Make sure the trimester variables are updated with new dates
	 
	 replace secondtrim_num = (pregstart_num+97) if ((pregstart_num+97)<pregend_num) & secondtrim_num==. & pregend_num!=.
	replace thirdtrim_num = (pregstart_num+188) if ((pregstart_num+188)<pregend_num) & thirdtrim_num==. & pregend_num!=.
	 
	 save "$Datadir\derived_data\pregnancy_cohort_final.dta", replace
	 
	keep patid pregid babypatid mblbabies
	save "$Datadir\derived_data\pregnancy_cohort_matids_pregids.dta", replace
	 
* Create datasets containing unique patid combinations due to multiple pregnancies in some patients

	use "$Datadir\derived_data\pregnancy_cohort_final.dta", clear
	bysort patid: gen bign = _N
	summ bign // n = 16 max number of pregnancies
	local maxpreg = r(max)
	sort patid pregstart_num
	drop bign
	
	forvalues x=1/`maxpreg' {
		
		 use "$Datadir\derived_data\pregnancy_cohort_final.dta", clear
		 bysort patid (pregid): keep if _n==`x'
		 save "$Datadir\derived_data\pregnancy_cohort_final_`x'.dta", replace
		
	 }
	 
* Check outcomes

	use "$Datadir\derived_data\pregnancy_cohort_final.dta", clear
	count
	tab updated_outcome
	 
/*
              Outcome of pregnancy (see |
                         documentation) |      Freq.     Percent        Cum.
----------------------------------------+-----------------------------------
                             Live birth |    734,938       59.02       59.02
                             Stillbirth |      3,211        0.26       59.28
              Live birth or still birth |        549        0.04       59.33
                            Miscarriage |    127,522       10.24       69.57
                                    TOP |     39,743        3.19       72.76
                           Probable TOP |     99,841        8.02       80.78
                                Ectopic |     11,158        0.90       81.67
                                  Molar |        975        0.08       81.75
                          Blighted ovum |        738        0.06       81.81
                       Unspecified loss |      6,495        0.52       82.33
Delivery based on a third trimester pre |      6,640        0.53       82.87
Delivery based on a late pregnancy reco |      1,973        0.16       83.03
                        Outcome unknown |    211,363       16.97      100.00
----------------------------------------+-----------------------------------
                                  Total |  1,245,146      100.00

*/
	
* Create variables to measure length of data history and follow-up

	restore
	
	gen data_history = crd_num - pregstart_num
	label variable data_history"Number of days since start of current registration to start of pregnancy"
	
	gen data_followup =.
	replace data_followup = lcd_num - pregstart_num if tod_num==. & deathdate_num==.
	replace data_followup = tod_num - pregstart_num if tod_num!=. & tod_num<=deathdate_num
	replace data_followup = deathdate_num - pregstart_num if deathdate_num!=. & deathdate_num<=tod_num
	label variable data_followup"Number of days from pregstart to earliest of lcd, tod or death" 
	summ lcd_num
	disp string(22421, "%td") // max value of lcd is 21may2021
	
* Identify why individuals have a negative length of follow-up

	count if data_followup < 0 // 0 with negative follow up
	count if 22421 < pregstart_num // 0 born after max last collection date value
	count if tod_num < pregstart_num // 0 born after transfer out date
	count if deathdate_num < pregstart_num // 0 born after death date of mum
	
	bysort pregyear: tab outcome
	br patid pregid pregstart_num pregend_num start_fup_cprd end_fup_CPRD data_followup outcome if pregyear>=2018 
	
	summ pregstart_num
	summ pregend_num
	
	* Save data
	
	keep patid pregid pracid data_followup data_history
	compress
	count // n = 1,245,146
	save "$Datadir\derived_data\pregnancy_cohort_follow_up_info.dta", replace
	
********************************************************************************

* Pulling variables out of HES

	do "$Dodir\2_eligibility\2f_hes vars.do"
	
	use "$Datadir\derived_data\pregnancy_cohort_final.dta", clear
	merge 1:1 patid pregid using "$Datadir\derived_data\pregreg_bw_gl.dta", nogen
	merge 1:1 patid pregid using "$Datadir\derived_data\labour_induction.dta", nogen
	
	merge 1:1 patid pregid using "$Datadir\covariates\new_parity.dta", keep(3) nogen
	count // 1,245,146
	tab parity, m // no missing
	
	merge 1:1 patid pregid using "$Datadir\covariates\new_gravidity.dta", keep(3) nogen
	count // 1,245,146
	tab gravidity_cat, m // no missing
	
	save "$Datadir\derived_data\pregnancy_cohort_final.dta", replace

********************************************************************************

* Worst case scenario analysis

	use "$Datadir\derived_data\pregnancy_cohort_final.dta", clear
	count
	
	tab updated_outcome
	
	sort gest_length
	br gest_length if updated_outcome==13
	
	replace pregstart_num = pregend_num - 84 if updated_outcome==13
	replace gest_length = 84 if updated_outcome==13
	
	gen unknown_outcome = 1 if updated_outcome==13
	
	recode updated_outcome 13=4
	
	tab updated_outcome
	
	save "$Datadir\derived_data\pregnancy_cohort_final_worstcase.dta", replace
	
	use "$Datadir\derived_data\pregnancy_cohort_final_worstcase.dta", clear
	bysort patid: gen bign = _N
	summ bign // n = 14 max number of pregnancies
	local maxpreg = r(max)
	sort patid pregstart_num
	drop bign
	
	forvalues x=1/`maxpreg' {
		
		 use "$Datadir\derived_data\pregnancy_cohort_final_worstcase.dta", clear
		 bysort patid (pregid): keep if _n==`x'
		 save "$Datadir\derived_data\pregnancy_cohort_final_worstcase_`x'.dta", replace
		
	 }
	 
* Final cohort for Karishma's project

	use "$Datadir\derived_data\pregnancy_cohort_final.dta", clear
	count
	
	keep patid pregid mblbabies babypatid updated_outcome outcome gestdays multiple_ev pregstart_num pregend_num
	
	rename updated_outcome outcome
	
	save "$Karishmadir\pregnancy_cohort_final.dta", replace

* Check how many are eligible for linkage to HES data

	preserve
	
	 merge m:1 patid using "$Datadir\formatted_linked_data\linkage_eligibility.dta", keep(3) nogen
	 gen linkage_hes = 1 if hes_apc_e==1
	 recode linkage_hes .=0
	 tab linkage_hes // 200,323 eligible for linkage with HES (49%)
	 
	restore

********************************************************************************

* Delete unnecessary datasets

	erase "$Tempdatadir\pregreg_hesoutcomesstep.dta"
	erase "$Tempdatadir\pregreg_hesoutcomesstep_extra_vars.dta"
	
	forvalues x=1/25 {
		
		erase "$Tempdatadir\pregreg_hesoutcomesstep_extra_vars_`x'.dta"
		
	}
	
	erase "$Tempdatadir\hes_maternity_all.dta"
	erase "$Tempdatadir\hes_icd_outcomes_delivery.dta"
	erase "$Tempdatadir\hes_icd_deliveries.dta"
	erase "$Tempdatadir\hes_deliveries_all.dta"
	erase "$Tempdatadir\hes_icd_losses.dta"
	erase "$Tempdatadir\hes_opcs_losses.dta"
	erase "$Tempdatadir\hes_opcs_outcomes_all.dta"
	erase "$Tempdatadir\hes_losses_all.dta"
	
	erase "$Tempdatadir\pregreg_preconflictstep.dta"
	erase "$Tempdatadir\pregreg_conflicts_currentlypregnantevents.dta"
	erase "$Tempdatadir\pregreg_conflicts_antenatalscanevents.dta"
	erase "$Tempdatadir\pregconflicts.dta"
	
	forvalues x=1/5 {
		
		erase "$Tempdatadir\EvScen4a_`x'.dta"
		
	}
	
	forvalues x=1/3 {
		foreach y in b c {
			
			erase "$Tempdatadir\EvScen4`y'_`x'.dta"
			
		}
	}
	
	foreach x in a b c d e {
		
		erase "$Tempdatadir\EvScen4`x'.dta"
		
	}
	
	erase "$Tempdatadir\pregreg_prehistoricalpregstep.dta"
	erase "$Tempdatadir\pregconflicts_pregendsort.dta"
	
	forvalues x=1/7 {
	
		erase "$Tempdatadir\Hist_Conflict_`x'.dta"
		
	}
	
********************************************************************************
	
* Stop logging

	log close
	
********************************************************************************
