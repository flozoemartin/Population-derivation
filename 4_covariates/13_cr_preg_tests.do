*********************************************************************************

* Pulling out pregnancy tests that occurred prior to the end of pregnancy among patients in the Pregnancy Register to investigate the likelihood of ascertainment bias among incident users of antidepressants in T1 as compared to prevalent users

* Author: Flo Martin

* Date: 03/04/2023

*********************************************************************************

* Datasets generated by this do-file

*  - $Datadir\covariates\preg_test_final.dta

*********************************************************************************

* Load in the Medical dictionary to make a codelist for pregnancy testing

	use "$Lookupsdir\stata\medical.dta", clear
		
	gen Z=lower(desc)
	drop desc
	rename Z desc
	
	gen preg_test = 1 if strmatch(desc, "*pregnancy test*")
	replace preg_test =. if strmatch(desc, "*negative*") & preg_test==1
	
	drop if preg_test!=1
	
	keep medcode readcode desc preg_test
	
	save "$Tocheckdir\pregnancy_test_codelist.dta", replace
	
*********************************************************************************

/* Create a dataset containing Clinical and Referral events
	
	use "$Datadir\formatted_cprd_data\All_Clinical.dta", clear
	append using "$Datadir\formatted_cprd_data\All_Referral.dta"
	keep patid medcode eventdate_num /*to reduce dataset size*/
	compress
	*drop if medcode<78 - what is this?
	save "$Tempdatadir\clinical_plus_referral.dta", replace*/
	
* Lift all events of pregnancy tests (except negative test results) from the Clinical and Referral Files

	use "$Tempdatadir\clinical_reduced.dta", clear
		
	merge m:1 medcode using "$Tocheckdir\pregnancy_test_codelist.dta", keep(3) nogen 
		
	sort patid
		
	save "$Tempdatadir\all_pregnancy_tests.dta", replace
	
*********************************************************************************

* Identify pregnancy tests that occurred up to 300 days prior to pregnancy end (the more reliable pregnancy date?)
	
	use "$Datadir\derived_data\pregnancy_cohort_final.dta", clear
	bysort patid: gen bign = _N
	summ bign // n = 14 max number of pregnancies
	local maxpreg = r(max)
	sort patid pregstart_num
	drop bign
	
	forvalues n=1/`maxpreg' {
			
		use "$Datadir\derived_data\pregnancy_cohort_final_`n'.dta", clear
		keep patid pregid pregstart_num 
			
		merge 1:m patid using "$Tempdatadir\all_pregnancy_tests.dta", keep(3) nogen
			
		keep if eventdate_num>=pregstart_num & eventdate_num<pregstart_num+84
			
		if _N>0 {
			
			sort patid eventdate_num
			duplicates drop
			drop medcode readcode 
				
			bysort patid pregid pregstart_num: egen _presseq=seq()
			tab _presseq
				
			keep if _presseq==1
			drop _presseq
				
		}
			
		else if _N==0 {	
				
			keep patid pregid
				
		}
			
		save "$Tempdatadir\preg_test_`n'.dta", replace
		
	}
	
* Append datasets for each pregnancy
		
	use "$Tempdatadir\preg_test_1.dta", clear
		
	forvalues n=2/`maxpreg' {
			
		append using "$Tempdatadir\preg_test_`n'.dta"
			
	}
		
	count // n = 13,641 pregnancies with a record of a pregnancy test within 300 days of the end of pregnancy
		
	order patid pregid eventdate_num desc pregstart_num preg_test
	
	merge 1:1 patid pregid using "$Datadir\derived_data\pregnancy_cohort_final.dta", keep(2 3) nogen
	recode preg_test .=0
	tab preg_test
		
	save "$Datadir\covariates\preg_test_final.dta", replace
	
*********************************************************************************

* Erase unnecessary datasets
	
	*erase "$Tempdatadir\clinical_plus_referral.dta"
	
	forvalues n=1/`maxpreg' {
			
			erase "$Tempdatadir\preg_test_`n'.dta"
			
		}
		
*********************************************************************************
