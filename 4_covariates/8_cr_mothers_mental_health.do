*******************************************************************************

* Maternal mental health conditions

* Author: Harriet Forbes (adapted by Flo Martin)

* Date: 30/09/2022

*******************************************************************************

* Datasets generated by this do-file

*  - $Datadir\covariates\long_term_mental_con_final.dta

*******************************************************************************

	use "$Datadir\derived_data\pregnancy_cohort_final.dta", clear
	bysort patid: gen bign = _N
	summ bign // n = 14 max number of pregnancies
	local maxpreg = r(max)
	sort patid pregstart_num
	drop bign

* Identify bipolar, schizophrenia and other at pregnancy start

	* append code lists

	use "$Codesdir\ReadCode_bipolar_sign_off_DR.dta", replace
	gen bipolar=1
	
	merge 1:1 medcode using "$Codesdir\ReadCode_psychosis_schizophrenia_signed_off_DR.dta"
	gen schizo=1 if _m==3 | _m==2
	drop _m
	
	duplicates list medcode readterm // n=0
	
	keep medcode bipolar schizo
	save "$Tempdatadir\all_codes_mental_health.dta", replace
	
*******************************************************************************
	
* Identify severe mental illness - bipolar, schizophrenia, psychosis - from primary care

	/* Lift all events relating to ASD from Clinical and referral Files
	
	use "$Datadir\formatted_cprd_data\All_Clinical.dta", clear
	append using "$Datadir\formatted_cprd_data\All_Referral.dta"
	keep patid medcode eventdate_num /*to reduce dataset size*/
	compress
	*drop if medcode<78 - what is this?
	save "$Tempdatadir\clinical_reduced.dta", replace*/
	
	foreach var in bipolar schizo {

		use "$Tempdatadir\all_codes_mental_health.dta", clear 
		keep if `var'==1 
		keep medcode `var'
		duplicates drop

		merge 1:m medcode using "$Tempdatadir\clinical_reduced.dta", keep(match) nogen
		rename eventdate `var'_dx_date
		drop if `var'_dx_date==.
		keep patid `var'_dx_date `var'
		bysort patid (`var'_dx_date): keep if _n==1 /*keeps earliest diagnosis: create 1 record per patient*/
		save "$Tempdatadir\codes_`var'_all", replace

	}
	
	foreach var in bipolar schizo {
		forvalues x=1/`maxpreg' {

			use "$Tempdatadir\codes_`var'_all", clear
			keep patid `var' `var'_dx_date
			merge 1:1 patid using "$Datadir\derived_data\pregnancy_cohort_final_`x'", keep(match) nogen
			keep patid `var' `var'_dx_date pregstart_num pregid
			save "$Tempdatadir\codes_`var'_`x'", replace

		}
	}
	
	foreach var in bipolar schizo {
		
		use "$Tempdatadir\codes_`var'_1", clear

		forvalues x=2/`maxpreg' {

			append using "$Tempdatadir\codes_`var'_`x'"
	
		}

		count
		drop if `var'_dx_date>pregstart_num /*drop if 1st Dx is after pregstart date*/
		keep patid pregid `var' `var'_dx_date

		count
		save "$Tempdatadir\final_`var'", replace

	}
	
	use "$Tempdatadir\final_bipolar", clear
	merge 1:1 patid pregid using "$Datadir\derived_data\pregnancy_cohort_final.dta", nogen
	merge 1:1 patid pregid using "$Tempdatadir\final_schizo", nogen
	keep patid pregid bipolar schizo
	replace bipolar = 0 if bipolar==.
	replace schizo = 0 if schizo ==. 
	save "$Datadir\covariates\long_term_mental_con_final", replace

*******************************************************************************

* Identify severe mental illness - bipolar, schizophrenia, psychosis - from secondary care

* Schizophrenia and psychosis

	use "$Datadir\formatted_linked_data\hes_diagnosis_epi.dta", clear
	
	replace icd = subinstr(icd, ".", "",.)
	
	merge m:1 icd using "$Codesdir\ICDCode_psychosis_schizophrenia.dta", keep(3) nogen
	
	save "$Datadir\indications\schizo_hes_diagnosis_epi.dta", replace
	
	use "$Datadir\formatted_linked_data\hes_diagnosis_hosp.dta", clear
	
	replace icd = subinstr(icd, ".", "",.)
	gen epistart_num = admidate_num
	
	merge m:1 icd using "$Codesdir\ICDCode_psychosis_schizophrenia.dta", keep(3) nogen
	
	save "$Datadir\indications\schizo_hes_diagnosis_hosp.dta", replace
	
	use "$Datadir\indications\schizo_hes_diagnosis_epi.dta", clear
	append using "$Datadir\indications\schizo_hes_diagnosis_hosp.dta"
	
	duplicates drop
	
	save "$Datadir\indications\hes_schizo.dta", replace
	
* Bipolar

	use "$Datadir\formatted_linked_data\hes_diagnosis_epi.dta", clear
	
	replace icd = subinstr(icd, ".", "",.)
	
	merge m:1 icd using "$Codesdir\ICDCode_bipolar_signed_off.dta", keep(3) nogen
	
	save "$Datadir\indications\bipolar_hes_diagnosis_epi.dta", replace
	
	use "$Datadir\formatted_linked_data\hes_diagnosis_hosp.dta", clear
	
	replace icd = subinstr(icd, ".", "",.)
	gen epistart_num = admidate_num
	
	merge m:1 icd using "$Codesdir\ICDCode_bipolar_signed_off.dta", keep(3) nogen
	
	save "$Datadir\indications\bipolar_hes_diagnosis_hosp.dta", replace
	
	use "$Datadir\indications\bipolar_hes_diagnosis_epi.dta", clear
	append using "$Datadir\indications\bipolar_hes_diagnosis_hosp.dta"
	
	duplicates drop
	
	save "$Datadir\indications\hes_bipolar.dta", replace
	
* Identify episodes that occurred relative to pregnancy start

	use "$Datadir\derived_data\pregnancy_cohort_final.dta", clear
	bysort patid: gen bign = _N
	summ bign // n = 14 max number of pregnancies
	local maxpreg = r(max)
	sort patid pregstart_num
	drop bign
	
* Creat binary variables for each patient's pregnancies whether they had a code for depression or anxiety in the periods of interest
		
		* Ever before the start of pregnancy
		
	foreach indic in schizo bipolar {
		forvalues n=1/`maxpreg' {
			
			use "$Datadir\derived_data\pregnancy_cohort_final_`n'.dta", clear
			keep patid pregid pregstart_num 
			
			merge 1:m patid using "$Datadir\indications\hes_`indic'.dta", keep(3) nogen
			
			keep patid pregid pregstart_num epistart_num icd
			
			keep if epistart_num<=pregstart_num
			
			if _N>0 {
			
				sort patid epistart_num
				by patid: egen _seq = seq()
				
				gsort + patid - epistart_num
				by patid: egen count_`indic'_ever_hes = seq()
				label variable count_`indic'_ever_hes "Number of `indic' codes ever before the start of pregnancy"
				
				sort patid epistart_num
				keep patid pregid icd count_`indic'

				reshape wide icd, i(patid pregid) j(count_`indic')
				
				gen `indic'_hes=1

				duplicates drop
				
			}
			
			else if _N==0 {	
				
				keep patid pregid
				
			}
			
			save "$Tempdatadir\ever_`n'.dta", replace
		
		}
		
		use "$Tempdatadir\ever_1.dta", clear
		
		forvalues n=2/`maxpreg' {
			
			append using "$Tempdatadir\ever_`n'.dta"
			
		}
		
		count
		save "$Tempdatadir\ever_`indic'_hes.dta", replace
		
	}
	
	use "$Tempdatadir\ever_schizo_hes.dta", clear
	merge 1:1 patid pregid using "$Tempdatadir\ever_bipolar_hes.dta"
	
	keep patid pregid schizo* bipolar*
	
	tab schizo_hes
	tab bipolar_hes
	
	merge 1:1 patid pregid using "$Datadir\covariates\long_term_mental_con_final", nogen
	
	tab schizo
	replace schizo = 1 if schizo_hes==1
	
	tab bipolar
	replace bipolar = 1 if bipolar_hes==1
	
	gen severe_mental_illness = 1 if schizo==1 | bipolar==1
	recode severe_mental_illness .=0 
	tab severe_mental_illness
	
	save "$Datadir\covariates\long_term_mental_con_final", replace
	
*******************************************************************************

* Delete unnecessary datasets

	foreach var in bipolar schizo {
		forvalues x=1/`maxpreg' {
			
			erase "$Tempdatadir\codes_`var'_`x'.dta"
			
		}
	}
	
	foreach var in bipolar schizo {
		
		erase "$Tempdatadir\final_`var'.dta"
		
	}

*******************************************************************************
