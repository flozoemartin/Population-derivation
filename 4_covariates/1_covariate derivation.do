********************************************************************************

* Covariate derivation

* Author: Flo Martin (adapted from scripts by Paul Madley-Dowd)

* Date: 15/08/2022

********************************************************************************

* Datasets generated by this do-file

  *  - $Datadir\covariates\year_of_pregnancy.dta  // year of pregnancy
  *  - $Datadir\covariates\preg_complications_final.dta  // history of pregnancy complications
  *  - $Datadir\covariates\matage.dta  // maternal age at the start of pregnancy
  *  - $Datadir\covariates\bmi_final.dta  // BMI around the start of pregnancy
  *  - $Datadir\covariates\marital.dta  // marital status
  *  - $Datadir\covariates\ethnicity_final.dta  // ethnicity
  *  - $Datadir\covariates\area_of_residence.dta  // area of residence
  *  - $Datadir\covariates\sep.dta  // socioeconomic position (IMD quintile)
  *  - $Datadir\covariates\long_term_mental_con_final.dta  // long term mental health conditions
  *  - $Datadir\covariates\long_term_neurodev_final.dta  // maternal neurodevelopmental conditions
  *  - $Datadir\covariates\gynae_problems_final.dta  // maternal gynaecological problems
  *  - $Datadir\covariates\long_term_health_con_final.dta  // long term health conditions
  *  - $Datadir\covariates\preg_test_final.dta  // evidence of pregnancy tests around the start of pregnancy
  *  - $Datadir\covariates\smoking_final.dta  // smoking around the start of pregnancy
  *  - $Datadir\covariates\alc_final.dta  // alcohol use around the start of pregnancy
  *  - $Datadir\covariates\illicitdruguse.dta  // illicit drug use in the 12 months before and during pregnancy
  *  - $Datadir\covariates\cprd_consultation_events.dta  // primary care utilisation in the 12 months before pregnancy
  *  - $Datadir\covariates\cprd_consult_events_preg.dta  // primary care utilisation during pregnancy
  *  - $Datadir\covariates\antipsychotics_prepreg_Rx.dta  // antipsychotic prescriptions in the 12 months before pregnancy
  *  - $Datadir\covariates\antipsychotics_preg_Rx.dta  // antipsychotic prescriptions during pregnancy
  *  - $Datadir\covariates\benzos_prepreg_Rx.dta  // benzodiazepine prescriptions in the 12 months before pregnancy
  *  - $Datadir\covariates\benzos_preg_Rx.dta  // benzodiazepine prescriptions during pregnancy
  *  - $Datadir\covariates\zdrugs_prepreg_Rx.dta  // Z-drug prescriptions in the 12 months before pregnancy
  *  - $Datadir\covariates\zdrugs_preg_Rx.dta  // Z-drug prescriptions during pregnancy
  *  - $Datadir\covariates\moodstabs_prepreg_Rx.dta  // ASM prescriptions in the 12 months before pregnancy
  *  - $Datadir\covariates\moodstabs_preg_Rx.dta  // ASM prescriptions during pregnancy
  *  - $Datadir\covariates\multivit_prepreg_Rx.dta  // multi-vitamin prescriptions in the 12 months before pregnancy
  *  - $Datadir\covariates\multivit_preg_Rx.dta  // multi-vitamin prescriptions during pregnancy
  *  - $Datadir\covariates\folic_prepreg_Rx.dta  // folic acid prescriptions in the 12 months before pregnancy
  *  - $Datadir\covariates\folic_preg_Rx.dta  // folic acid prescriptions during pregnancy
  *  - $Datadir\covariates\antiemetic_prepreg_Rx.dta  // antiemetic prescriptions in the 12 months before pregnancy
  *  - $Datadir\covariates\antiemetic_preg_Rx.dta  // antiemetic prescriptions during pregnancy
  *  - $Datadir\covariates\teratogen_prepreg_Rx.dta  // teratogen prescriptions in the 12 months before pregnancy
  *  - $Datadir\covariates\teratogen_preg_Rx.dta  // teratogen prescriptions during pregnancy

********************************************************************************
	
* Install required packages

	ssc install datacheck

* Start logging

	log using "$Logdir\4_covariates\1_covariate derivation.txt", replace

********************************************************************************

* Maternal age

	use "$Datadir\derived_data\pregnancy_cohort_final.dta", clear
	keep patid pregid pregstart_num 
	
	* ... and onto the patient information
	
	merge m:1 patid using "$Datadir\formatted_cprd_data\All_Patient.dta", keep(3) nogen

	* Derive estimated DOB as the mid point of the year (1st July) of their year of birth 

	gen eDOB = mdy(7,1,yob)
	gen matage_at_pregstart_days = pregstart_num - eDOB  
	gen matage_at_pregstart_years = floor(matage_at_pregstart_days / 365.25)
	
	* Clean variable for implausible maternal ages 

	replace matage_at_pregstart_years = . if (matage_at_pregstart_years>100) // 0 individuals with ages >100
	
* Derive categorical measure for descriptive statistics 

	recode matage_at_pregstart_years (0/17 = 1) (18/24 = 2) (25/29 = 3) (30/34 = 4) (35/100 = 5), gen(matage_cat)

	* Create labels 

	label variable matage_at_pregstart_years "Maternal age in years at pregnancy start (assumes DOB was 1 July)"
	label variable matage_cat "Maternal age (years)"
	label define lb_matage_cat 1 "<18" 2 "18-24" 3 "25-29" 4 "30-34"  5 ">=35"
	label values matage_cat lb_matage_cat

* Save dataset 

	rename matage_at_pregstart_years matage		
	keep patid pregid matage matage_cat yob
	
	count
	tab matage_cat, m
	
	save "$Datadir\covariates\matage.dta", replace 
	
********************************************************************************

* Marital status

	* Load in marital status lookup

		use "$Lookupsdir\stata\MAR.dta", clear
		rename code marital
		save "$Lookupsdir\stata\marital_lookup.dta", replace
		
	* Load in patient data
	
		use "$Datadir\derived_data\pregnancy_cohort_final.dta", clear
		merge m:1 patid using "$Datadir\formatted_cprd_data\All_Patient.dta", keep(3) nogen
		
		* Merge onto marital status lookup
		
		merge m:1 marital using "$Lookupsdir\stata\marital_lookup.dta", nogen
		
	* Regroup marital groups - single, married or civil partnered (inc. cohabiting and in stable relationship), separated or widowed or divorced and unknown
	
		tab maritalstatus
		gen marital_deriv = maritalstatus
		replace marital_deriv = "Unknown" if maritalstatus=="Data Not Entered" 
		replace marital_deriv = "In a partnership" if inlist(maritalstatus, "Civial Partnership", "Co-habiting", "Engaged", "Married", "Remarried", "Stable relationship")
		replace marital_deriv = "Previously in a partnership" if inlist(maritalstatus, "Divorced", "Separated", "Widowed")
		tab marital_deriv
		encode marital_deriv, gen(marital_derived)
		tab marital_derived, nolabel
		
	* Create labels
	
		label variable marital_derived "Marital status"
		
	* Save dataset
	
		keep patid pregid marital_derived
		
		count
		tab marital_derived, m
		
		save "$Datadir\covariates\marital.dta", replace
		
********************************************************************************

* Ethnicity

	do "$Dodir\4_covariates\2a_clean_ethnicity_codes_in_HES.do"
	do "$Dodir\4_covariates\2b_clean_ethnicity_codes_in_CPRD.do"
	
	* update formatting of variable 
	
	use "$Tempdatadir\ethnicity_final.dta", clear
	label variable eth5 "Ethnicity"
	label define lb_eth5 0 "White" 1 "South Asian" 2 "Black" 3 "Other" 4 "Mixed" 5 "Missing or not Stated"
	label values eth5 lb_eth5
	replace eth5 = 5 if eth5 == . 

	order patid pregid eth5
	
	count 
	tab eth5, m
	
	save "$Datadir\covariates\ethnicity_final.dta", replace
	
********************************************************************************

* Area of residence

	* Load in lookup

		use "$Lookupsdir\stata\PRG.dta", clear
		rename code region
		save "$Lookupsdir\stata\region_lookup.dta", replace
		
	* Load in pregnancy cohort
	
		use "$Datadir\derived_data\pregnancy_cohort_final.dta", clear
		
	* derive practice ID
		
		tostring patid, gen(patid_str) format(%12.0f) force
		gen pracid_str = substr(patid_str, -5,5)
		destring pracid_str, gen(pracid)

	* merge on practice information 

		merge m:1 pracid using "$Datadir\formatted_cprd_data\All_Practice.dta", keep(3) nogen 
		
	* merge on region lookup

		merge m:1 region using "$Lookupsdir\stata\region_lookup.dta", nogen 
		
	* turn into numeric variable

		encode practiceregion, gen(AreaOfResidence)

		keep patid pregid AreaOfResidence	
		save "$Datadir\covariates\area_of_residence.dta", replace 
	
********************************************************************************

* Socioeconomic position using Index of Multiple Deprivation
	
	use "$Datadir\derived_data\pregnancy_cohort_final.dta", clear // take pregnancy cohort mothers

	* derive practice ID
	tostring patid, gen(patid_str) format(%12.0f) force
	gen pracid_str = substr(patid_str, -5,5)
	destring pracid_str, gen(pracid)

	* merge on IMD by practice ID 
	merge m:1 pracid using "$Datadir\formatted_linked_data\imd_practice.dta", nogen keep(1 3)

	* check missing data 
	count if !missing(imd_practice) // 1,245,146 with practice level IMD information
	
	save "$Datadir\covariates\sep.dta" , replace 
		
********************************************************************************

* Maternal smoking, alcohol and body mass index - this information will be collected closest to the start of pregnancy

	do "$Dodir\4_covariates\3_alc_smok_bmi.do"
	
	* Update missing values  

		use "$Datadir\covariates\smoking_final.dta", clear
		replace smokstatus = 3 if smokstatus == . 	
		label define lb_smokstatus 3 "No information", modify
		save "$Datadir\covariates\smoking_final.dta", replace

		use "$Datadir\covariates\bmi_final.dta", clear
		replace bmi_cat = 4 if bmi_cat == . 
		label define lb_bmi 4 "No information", modify
		save "$Datadir\covariates\bmi_final.dta", replace

		use "$Datadir\covariates\alc_final.dta", clear
		replace hazardous_drinking = 0 if hazardous_drinking == . 
		save "$Datadir\covariates\alc_final.dta", replace

********************************************************************************

* Prior healthcare utilisation in the year before pregnancy

* load consultation files 

	use "$Datadir\formatted_cprd_data\All_Consultation.dta", clear
	count // 776,793,754

	* remove all consultations that do not involve an interaction with a health care professional

	foreach x in 5 12 13 14 15 16 17 19 23 24 25 26 29 41 42 43 44 45 47 48 49 51 52 53 54 56 57 58 59 60 {
    
		tab constype if constype == `x'
		drop if constype ==`x'

	}
	
	count // 46,898,152
	
* remove all consultations from before the date of 1 year before the first pregnancy in cohort (1Jan1995)

	keep if eventdate_num >= date("1Jan1994","DMY") // 2 million consultations removed
	duplicates drop patid eventdate_num, force // keep consultations on different days only
	save "$Tempdatadir\Consultation_f2f.dta", replace 


* load pregnancy cohort IDs

	use "$Datadir\derived_data\pregnancy_cohort_final.dta", clear
	keep patid pregid pregstart_num 

	* create a sequence variable for pregnancies 
	sort patid 
	egen _seq=seq(), by(patid)

* merge on consultation information 
	
	summ _seq
	local maxpreg = r(max)
	/*forvalues x = 1(1)`maxpreg' {
	
		disp "x = " `x'
		preserve 
		keep if _seq==`x' 
		merge 1:m patid using "$Tempdatadir\Consultation_f2f.dta", keep(3) nogen 
		keep if eventdate_num < pregstart_num & eventdate_num > pregstart_num - 365  // retain only events in the year before pregnancy

		if _N > 0 { // some groups may have no events in year before pregnancy
			
			egen CPRD_consultation_events = count(eventdate_num), by(patid) // count number of events for each patient
			duplicates drop patid, force // keep only first instance of each patient
			keep patid pregid CPRD_consultation_events	
		}
	
		if _N == 0 {
		
			keep patid pregid 
		}
	
		save "$Tempdatadir\CPRD_consultation_events_preg`x'.dta", replace 
		restore 
	
	}*/

* append all CPRD consultation datasets together 

	use "$Tempdatadir\CPRD_consultation_events_preg1.dta", clear
	forvalues x = 2(1)`maxpreg' {
		
		append using "$Tempdatadir\CPRD_consultation_events_preg`x'.dta"

	}

* merge back onto pregnancy register

	merge 1:1 patid pregid using "$Datadir\derived_data\pregnancy_cohort_final.dta", keep(2 3) nogen 

* set missing values to 0

	replace CPRD_consultation_events = 0 if CPRD_consultation_events == . 

* create categorical variable 

	recode CPRD_consultation_events (0 = 0) (1/3 = 1) (4/10 = 2) (11/1000 = 3), gen(CPRD_consultation_events_cat)

* label variables

	label variable CPRD_consultation_events "Number of CPRD consultation events in year before pregnancy"
	label variable CPRD_consultation_events_cat "Number of CPRD consultation events in year before pregnancy"
	label define lb_CPRD_consultation_events 0 "0" 1 "1-3" 2 "4-10" 3 ">10"
	label values CPRD_consultation_events_cat lb_CPRD_consultation_events

* Save CPRD information 
	
	save "$Datadir\covariates\cprd_consultation_events.dta" , replace 

*********************************************************************************

* Health care utilization during pregnancy

* load consultation files 

	use "$Datadir\formatted_cprd_data\All_Consultation.dta", clear

* remove all consultations that do not involve an interaction with a health care professional

	foreach x in 5 12 13 14 15 16 17 19 23 24 25 26 29 41 42 43 44 45 47 48 49 51 52 53 54 56 57 58 59 60 {
    
		tab constype if constype == `x'
		drop if constype ==`x'

	}
	
* remove all consultations from before the date of the first pregnancy in cohort (1Jan1995)

	keep if eventdate_num >= date("1Jan1995","DMY") // 7 million consultations removed
	duplicates drop patid eventdate_num, force // keep consultations on different days only
	save "$Tempdatadir\Consultation_preg.dta", replace 


* load pregnancy cohort IDs

	use "$Datadir\derived_data\pregnancy_cohort_final.dta", clear 
	keep patid pregid pregstart_num pregend_num

* create a sequence variable for pregnancies 

	sort patid
	egen _seq=seq(), by(patid)
	
	summ _seq
	local maxpreg = r(max)

/* merge on consultation information 

	forvalues x = 1(1)`maxpreg' {
	
		disp "x = " `x'
		preserve 
		keep if _seq==`x' 
		merge 1:m patid using "$Tempdatadir\Consultation_preg.dta", keep(3) nogen 
		keep if eventdate_num < pregend_num & eventdate_num > pregstart_num   // retain only events in  pregnancy

		if _N > 0 { // some groups may have no events  pregnancy
		
			egen CPRD_consult_events_preg = count(eventdate_num), by(patid) // count number of events for each patient
			duplicates drop patid, force // keep only first instance of each patient
			keep patid pregid CPRD_consult_events_preg	
	
		}
	
		if _N == 0 {
		
			keep patid pregid 
		
		}
	
		save "$Tempdatadir\CPRD_consult_events_preg`x'.dta", replace 
		restore 

	}*/

* append all CPRD consultation datasets together 

	use "$Tempdatadir\CPRD_consult_events_preg1.dta", clear
	forvalues x = 2(1)`maxpreg' {
	
		append using "$Tempdatadir\CPRD_consult_events_preg`x'.dta"

	}

* merge back onto pregnancy register

	merge 1:1 patid pregid using "$Datadir\derived_data\pregnancy_cohort_final.dta", keep(2 3) nogen 

* set missing values to 0

	replace CPRD_consult_events_preg = 0 if CPRD_consult_events_preg == . 

* create categorical variable 

	recode CPRD_consult_events_preg (0 = 0) (1/3 = 1) (4/10 = 2) (11/1000 = 3), gen(CPRD_consult_events_preg_cat)

* label variables

	label variable CPRD_consult_events_preg "Number of CPRD consultation events during pregnancy"
	label variable CPRD_consult_events_preg_cat "Number of CPRD consultation events during pregnancy"
	label define lb_CPRD_consult_events_preg 0 "0" 1 "1-3" 2 "4-10" 3 ">10"
	label values CPRD_consult_events_preg_cat lb_CPRD_consult_events_preg

* Save CPRD information 

	save "$Datadir\covariates\cprd_consult_events_preg.dta" , replace 

*********************************************************************************

* Other prescriptions during pregnancy

	do "$Dodir\4_covariates\4_cr_extract_mental_health_Rx.do"
	do "$Dodir\4_covariates\5_cr_extract_other_Rx.do"
	
*********************************************************************************

* Calendar year of pregnancy

	use "$Datadir\derived_data\pregnancy_cohort_final.dta", clear 
	keep patid pregid pregstart_num 

	* Generate year of pregnancy start 

	gen pregstart_year = year(pregstart_num)

	* Recode into categories 

	recode pregstart_year (1995/2000 = 1) (2001/2006 = 2) (2007/2012 = 3) (2013/2018 = 4), gen(pregstart_year_cat)
	label define lb_yearcat 1 "1995-2000" 2 "2001-2006" 3 "2007-2012" 4 	"2013-2018"
	label values pregstart_year_cat lb_yearcat

	* Create label 

	label variable pregstart_year "Year of pregnancy start"
	label variable pregstart_year_cat "Year of pregnancy start"

* Save file 
	
	keep patid pregid pregstart_year*
	save "$Datadir\covariates\year_of_pregnancy.dta" , replace 
	
********************************************************************************	

/* Gravidity - number of times one has been pregnant
	
	use "$Datadir\derived_data\pregnancy_cohort_matids_pregids.dta", clear // take pregnancy cohort mothers

	* merge on pregnancy register information 

	merge 1:1 patid pregid using "$Datadir\formatted_linked_data\pregnancyregister.dta", keep(3) 
	keep patid pregid pregnumber // taking the pregnancy number as gravidity 

	recode pregnumber (1 = 0) (2 = 1) (3 = 2) (4/100 = 3), gen(gravidity_cat)
	label variable gravidity_cat "Gravidity"
	label define lb_graviditycat 0 "0" 1 "1" 2 "2" 3 "3+"
	label values gravidity_cat lb_graviditycat

	save "$Datadir\covariates\old_gravidity.dta" , replace 
	
* Gravidity history - loss outcomes of previous pregnancies 

	use "$Datadir\derived_data\pregnancy_cohort_final.dta", clear // take pregnancy cohort mothers

	* merge on pregnancy register information 

	merge 1:1 patid pregid using "$Datadir\formatted_linked_data\pregnancyregister.dta", keep(3) 
	keep patid pregid pregnumber updated_outcome 
	
	* Create index variable for reshaping
					
	bysort patid: egen _presseq=seq()
	summ _presseq
	local _presseqmax = r(max)
	
	reshape wide pregid pregnumber updated_outcome, i(patid) j(_presseq)
	
	foreach x in 1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 17 18 19 20 {
	
		gen grav_hist_sa`x' = 1 if updated_outcome`x'==4
		gen grav_hist_sb`x' = 1 if updated_outcome`x'==2 | updated_outcome`x'==3
		gen grav_hist_top`x' = 1 if updated_outcome`x'==5 | updated_outcome`x'==6
		gen grav_hist_otherloss`x' = 1 if updated_outcome`x'==7 | updated_outcome`x'==8 | updated_outcome`x'==9
		
	}
	
	foreach x in sa sb top otherloss {
		
		rename grav_hist_`x'1 grav_hist_`x'_2
		rename grav_hist_`x'2 grav_hist_`x'_3
		rename grav_hist_`x'3 grav_hist_`x'_4
		rename grav_hist_`x'4 grav_hist_`x'_5
		rename grav_hist_`x'5 grav_hist_`x'_6
		rename grav_hist_`x'6 grav_hist_`x'_7
		rename grav_hist_`x'7 grav_hist_`x'_8
		rename grav_hist_`x'8 grav_hist_`x'_9
		rename grav_hist_`x'9 grav_hist_`x'_10
		rename grav_hist_`x'10 grav_hist_`x'_11
		rename grav_hist_`x'11 grav_hist_`x'_12
		rename grav_hist_`x'12 grav_hist_`x'_13
		rename grav_hist_`x'13 grav_hist_`x'_14
		rename grav_hist_`x'14 grav_hist_`x'_15
		rename grav_hist_`x'15 grav_hist_`x'_16
		rename grav_hist_`x'16 grav_hist_`x'_17
		rename grav_hist_`x'17 grav_hist_`x'_18
		rename grav_hist_`x'18 grav_hist_`x'_19
		rename grav_hist_`x'19 grav_hist_`x'_20
		rename grav_hist_`x'20 grav_hist_`x'_21
			
	}
	

	foreach y in `y' sa sb top otherloss {
		foreach x in 3 4 5 6 7 8 9 10 11 12 13 14 15 16 17 18 19 20 21 {
	
			replace grav_hist_`y'_`x' = 1 if grav_hist_`y'_2==1
		
		}
	
		foreach x in 4 5 6 7 8 9 10 11 12 13 14 15 16 17 18 19 20 21 {
	
			replace grav_hist_`y'_`x' = 1 if grav_hist_`y'_3==1
	
		}
	
		foreach x in 5 6 7 8 9 10 11 12 13 14 15 16 17 18 19 20 21 {
	
			replace grav_hist_`y'_`x' = 1 if grav_hist_`y'_4==1
		
		}
	
		foreach x in 6 7 8 9 10 11 12 13 14 15 16 17 18 19 20 21 {
	
			replace grav_hist_`y'_`x' = 1 if grav_hist_`y'_5==1
		
		}
	
		foreach x in 7 8 9 10 11 12 13 14 15 16 17 18 19 20 21 {
	
			replace grav_hist_`y'_`x' = 1 if grav_hist_`y'_6==1
		
		}
		
		foreach x in 8 9 10 11 12 13 14 15 16 17 18 19 20 21 {
	
			replace grav_hist_`y'_`x' = 1 if grav_hist_`y'_7==1
		
		}
	
		foreach x in 9 10 11 12 13 14 15 16 17 18 19 20 21 {
	
			replace grav_hist_`y'_`x' = 1 if grav_hist_`y'_8==1
		
		}
	
		foreach x in 10 11 12 13 14 15 16 17 18 19 20 21 {
	
			replace grav_hist_`y'_`x' = 1 if grav_hist_`y'_9==1
		
		}
	
		foreach x in 11 12 13 14 15 16 17 18 19 20 21 {
	
			replace grav_hist_`y'_`x' = 1 if grav_hist_`y'_10==1
		
		}

		foreach x in 12 13 14 15 16 17 18 19 20 21 {
	
			replace grav_hist_`y'_`x' = 1 if grav_hist_`y'_11==1
		
		}
	
		foreach x in 13 14 15 16 17 18 19 20 21 {
	
			replace grav_hist_`y'_`x' = 1 if grav_hist_`y'_12==1
		
		}
	
		foreach x in 14 15 16 17 18 19 20 21 {
	
			replace grav_hist_`y'_`x' = 1 if grav_hist_`y'_13==1
		
		}
	
		foreach x in 15 16 17 18 19 20 21 {
	
			replace grav_hist_`y'_`x' = 1 if grav_hist_`y'_14==1
		
		}
	
		foreach x in 16 17 18 19 20 21 {
	
			replace grav_hist_`y'_`x' = 1 if grav_hist_`y'_15==1
		
		}
	
		foreach x in 17 18 19 20 21 {
	
			replace grav_hist_`y'_`x' = 1 if grav_hist_`y'_16==1
		
		}
	
		foreach x in 18 19 20 21 {
	
			replace grav_hist_`y'_`x' = 1 if grav_hist_`y'_17==1
		
		}
	
		foreach x in 19 20 21 {
	
			replace grav_hist_`y'_`x' = 1 if grav_hist_`y'_18==1
		
		}
	
		foreach x in 20 21 {
	
			replace grav_hist_`y'_`x' = 1 if grav_hist_`y'_19==1
		
		}
	
		replace grav_hist_`y'_21 = 1 if grav_hist_`y'_20==1
	
	}
	
	reshape long pregid pregnumber updated_outcome grav_hist_sa_ grav_hist_sb_ grav_hist_top_ grav_hist_otherloss_, i(patid) j(_presseq)
	count
	
	drop if pregid==. & updated_outcome==.
	count
	
	tab grav_hist_sa_
	replace grav_hist_sa_ = 0 if grav_hist_sa_!=1
	
	tab grav_hist_sb_
	replace grav_hist_sb_ = 0 if grav_hist_sb_!=1
	
	tab grav_hist_top_
	replace grav_hist_top_ = 0 if grav_hist_top_!=1
	
	tab grav_hist_otherloss_
	replace grav_hist_otherloss_ = 0 if grav_hist_otherloss_!=1

	save "$Datadir\covariates\old_gravhist.dta" , replace 
	
********************************************************************************	

* Parity - now use live birth to define parity (number of children)

	use "$Datadir\derived_data\pregnancy_cohort_final.dta", clear // take pregnancy cohort mothers

	* merge on pregnancy register information 

	merge 1:1 patid pregid using "$Datadir\formatted_linked_data\pregnancyregister.dta", keep(3) 
	keep patid pregid updated_outcome pregnumber pregstart_num multiple_ev
	
	tab updated_outcome
	tab updated_outcome, nol
	keep if updated_outcome==1 | updated_outcome==2 | updated_outcome==3 | updated_outcome==11 | updated_outcome==12
	
	sort patid pregstart_num
	bysort patid: egen parity=seq()
	summ parity
	
	tab parity
	
	merge 1:1 patid pregid using "$Datadir\derived_data\pregnancy_cohort_final.dta"
	keep patid pregid updated_outcome parity pregnumber
	
	drop if patid==. | pregid==.
	drop if updated_outcome==. | updated_outcome==13
	sort patid pregid pregnumber // for pregnancy losses parity isn't carried forward - need to apply the same method as for gravidity history to parity
	
	bysort patid: egen _presseq=seq()
	drop pregnumber
	
	reshape wide pregid parity updated_outcome, i(patid) j(_presseq)
	
	replace parity2 = parity1 if parity2==. & updated_outcome2!=.
	replace parity3 = parity2 if parity3==. & updated_outcome3!=.
	replace parity4 = parity3 if parity4==. & updated_outcome4!=.
	replace parity5 = parity4 if parity5==. & updated_outcome5!=.
	replace parity6 = parity5 if parity6==. & updated_outcome6!=.
	replace parity7 = parity6 if parity7==. & updated_outcome7!=.
	replace parity8 = parity7 if parity8==. & updated_outcome8!=.
	replace parity9 = parity8 if parity9==. & updated_outcome9!=.
	replace parity10 = parity9 if parity10==. & updated_outcome10!=.
	replace parity11 = parity10 if parity11==. & updated_outcome11!=.
	replace parity12 = parity11 if parity12==. & updated_outcome12!=.
	replace parity13 = parity12 if parity13==. & updated_outcome13!=.
	replace parity14 = parity13 if parity14==. & updated_outcome14!=.
	replace parity15 = parity14 if parity15==. & updated_outcome15!=.
	replace parity16 = parity15 if parity16==. & updated_outcome16!=.
	replace parity17 = parity16 if parity17==. & updated_outcome17!=.
	replace parity18 = parity17 if parity18==. & updated_outcome18!=.
	replace parity19 = parity18 if parity19==. & updated_outcome19!=.
	
	reshape long pregid updated_outcome parity, i(patid) j(_presseq)
	drop if pregid==.
	count
	
	merge 1:1 patid pregid using "$Datadir\formatted_linked_data\pregnancyregister.dta", keep(3) keepusing(pregnumber)
	drop _merge
	replace parity = 0 if parity==. & pregnumber==1 // only for those who we know that they haven't had any prior pregnancies
	
	reshape wide pregid parity updated_outcome pregnumber, i(patid) j(_presseq)
	
	replace parity1 = 0 if (updated_outcome2==1 | updated_outcome3==1 | updated_outcome4==1 | updated_outcome5==1 | updated_outcome6==1 | updated_outcome7==1 | updated_outcome8==1 | updated_outcome9==1 | updated_outcome10==1 | updated_outcome11==1 | updated_outcome12==1 | updated_outcome13==1 | updated_outcome14==1 | updated_outcome15==1 | updated_outcome16==1 | updated_outcome17==1 | updated_outcome18==1 | updated_outcome19==1) & parity1==. // for those who have subsequent live births (thus categorising as having one child at their first live birth)
	
	reshape long pregid updated_outcome parity pregnumber, i(patid) j(_presseq)
	drop _presseq
	drop if pregid==.
	
	gen parity_cat = 0 if parity==0 | parity==1
	replace parity_cat = 1 if parity==2
	replace parity_cat = 2 if parity==3
	replace parity_cat = 3 if parity>=4 & parity!=.
	
	label define parity_lb 1"1" 2"2" 3"3+"
	label values parity_cat parity_lb
	
	tab parity_cat
	
	save "$Datadir\covariates\old_parity.dta" , replace
	
*/
	
********************************************************************************	

* Recreational drug use

	do "$Dodir\4_covariates\6_cr_illicit_drug_use.do"
	
********************************************************************************

* Maternal physical health conditions

	do "$Dodir\4_covariates\7_cr_mothers_physical_conditions.do"
	
********************************************************************************

* Maternal mental health conditions

	do "$Dodir\4_covariates\8_cr_mothers_mental_health.do"
	
********************************************************************************

* Maternal neurodevelopmental health

	do "$Dodir\4_covariates\9_cr_mothers_neurodevelopmental_health.do"
	
********************************************************************************

* Maternal teratogen exposure

	do "$Dodir\4_covariates\10_cr_teratogens.do"
	
********************************************************************************

* Pregnancy complications - antenatal haemorrhage, fetal growth restriction and preeclampsia

	do "$Dodir\4_covariates\11_cr_preg_complications.do"
	
********************************************************************************

* Gynaecological problems - endometriosis and polycystic ovary syndrome

	do "$Dodir\4_covariates\12_cr_gynae_problems.do"
	
********************************************************************************

* Pregnancy tests at the GP 

	do "$Dodir\4_covariates\13_cr_preg_tests.do"
	
********************************************************************************

* Collate all covariates into a single dataset

	use "$Datadir\derived_data\pregnancy_cohort_final.dta", clear
	keep patid pregid
	
	* Pregnancy characteristics
	
	merge 1:1 patid pregid using "$Datadir\covariates\year_of_pregnancy.dta", nogen
	merge 1:1 patid pregid using "$Datadir\covariates\preg_complications_final", nogen
	
	* Maternal characteristics
	
	merge 1:1 patid pregid using "$Datadir\covariates\matage.dta", nogen
	merge 1:1 patid pregid using "$Datadir\covariates\bmi_final.dta", nogen
	merge 1:1 patid pregid using "$Datadir\covariates\marital.dta", nogen
	merge 1:1 patid pregid using  "$Datadir\covariates\ethnicity_final", nogen
	merge 1:1 patid pregid using "$Datadir\covariates\area_of_residence.dta", nogen
	merge 1:1 patid pregid using "$Datadir\covariates\sep.dta", nogen
	merge 1:1 patid pregid using "$Datadir\covariates\long_term_mental_con_final", nogen
	merge 1:1 patid pregid using "$Datadir\covariates\long_term_neurodev_final", nogen
	merge 1:1 patid pregid using "$Datadir\covariates\gynae_problems_final", nogen
	merge 1:1 patid pregid using "$Datadir\covariates\long_term_health_con_final", nogen
	merge 1:1 patid pregid using "$Datadir\covariates\preg_test_final", nogen
	merge 1:1 patid pregid using "$Datadir\covariates\new_gravhist.dta", keep(3) nogen
	
	* Lifestyle exposures
	
	merge 1:1 patid pregid using "$Datadir\covariates\smoking_final.dta", nogen
	merge 1:1 patid pregid using "$Datadir\covariates\alc_final.dta", nogen
	merge 1:1 patid pregid using "$Datadir\covariates\illicitdruguse.dta", nogen
	
	* Year before pregnancy
	
	merge 1:1 patid pregid using "$Datadir\covariates\cprd_consultation_events.dta", nogen
	merge 1:1 patid pregid using "$Datadir\covariates\antipsychotics_prepreg_Rx.dta", nogen 
	merge 1:1 patid pregid using "$Datadir\covariates\benzos_prepreg_Rx.dta", nogen
	merge 1:1 patid pregid using "$Datadir\covariates\zdrugs_prepreg_Rx.dta", nogen
	merge 1:1 patid pregid using "$Datadir\covariates\moodstabs_prepreg_Rx.dta", nogen
	merge 1:1 patid pregid using "$Datadir\covariates\multivit_prepreg_Rx.dta", nogen
	merge 1:1 patid pregid using "$Datadir\covariates\folic_prepreg_Rx.dta", nogen
	merge 1:1 patid pregid using "$Datadir\covariates\antiemetic_prepreg_Rx.dta", nogen
	merge 1:1 patid pregid using "$Datadir\covariates\teratogen_prepreg_Rx.dta", nogen 
	
	* During pregnancy
	
	merge 1:1 patid pregid using "$Datadir\covariates\cprd_consult_events_preg.dta", nogen
	merge 1:1 patid pregid using "$Datadir\covariates\antipsychotics_preg_Rx.dta", nogen
	merge 1:1 patid pregid using "$Datadir\covariates\benzos_preg_Rx.dta", nogen 
	merge 1:1 patid pregid using "$Datadir\covariates\zdrugs_preg_Rx.dta", nogen
	merge 1:1 patid pregid using "$Datadir\covariates\moodstabs_preg_Rx.dta", nogen 
	merge 1:1 patid pregid using "$Datadir\covariates\multivit_preg_Rx.dta", nogen 
	merge 1:1 patid pregid using "$Datadir\covariates\folic_preg_Rx.dta", nogen
	merge 1:1 patid pregid using "$Datadir\covariates\antiemetic_preg_Rx.dta", nogen 
	merge 1:1 patid pregid using "$Datadir\covariates\teratogen_preg_Rx.dta", nogen 
	
	* Save covariate information

	save "$Datadir\covariates\pregnancy_cohort_covariates.dta", replace

********************************************************************************		

* Stop logging

	log close
	
********************************************************************************
