*******************************************************************************

* Maternal physical health conditions

* Author: Harriet Forbes (adapted by Flo Martin)

* Date: 26/09/2022

*******************************************************************************

* Datasets generated by this do-file

*  - $Datadir\covariates\long_term_health_con_final.dta

*******************************************************************************

	use "$Datadir\derived_data\pregnancy_cohort_final.dta", clear
	bysort patid: gen bign = _N
	summ bign // n = 14 max number of pregnancies
	local maxpreg = r(max)
	sort patid pregstart_num
	drop bign

* Identify asthma, CKD, diabetes, hypothyroidism and hypertension before pregnancy start

	* append code lists

	use "$Codesdir\ReadCode_asthma_signed_off_DR.dta", replace
	gen asthma=1
	
	merge 1:1 medcode using "$Codesdir\READCode_CKD_signed_off_HF"
	gen ckd=1 if _m==3 | _m==2
	drop _m
	
	merge 1:1 medcode using "$Codesdir\READCode_diabetes_signed_off_DR"
	gen diab=1 if _m==3 | _m==2
	drop _m
	
	merge 1:1 medcode using "$Codesdir\READcodes_hypertension_signed_off_DR"
	gen hypbp=1 if _m==3 | _m==2
	drop _m
	
	merge 1:1 medcode using "$Codesdir\hypothyroid_codelist.dta"
	drop _m
	
	* Autoimmune codelist not yet signed off
	
	duplicates list medcode readterm // n=0
	
	keep medcode asthma ckd diab hypbp hypothy
	save "$Tempdatadir\all_codes_physical_health.dta", replace
	
* Identify asthma, diabetes and copd records at cohort entry

	* Lift all events relating to ASD from Clinical and referral Files
	
	use "$Datadir\formatted_cprd_data\All_Clinical.dta", clear
	append using "$Datadir\formatted_cprd_data\All_Referral.dta"
	keep patid medcode eventdate_num /*to reduce dataset size*/
	compress
	drop if medcode<78
	save "$Tempdatadir\clinical_reduced.dta", replace
	
	foreach var in asthma ckd diab hypbp hypothy {

		use "$Tempdatadir\all_codes_physical_health.dta", clear 
		keep if `var'==1 
		keep medcode `var'
		duplicates drop

		merge 1:m medcode using "$Tempdatadir\clinical_reduced.dta", keep(match) nogen
		rename eventdate `var'_dx_date
		drop if `var'_dx_date==.
		keep patid `var'_dx_date `var'
		bysort patid (`var'_dx_date): keep if _n==1 /*keeps earliest diagnosis: create 1 record per patient*/
		save "$Tempdatadir\codes_`var'_all", replace

	}
	
	foreach var in asthma ckd diab hypbp hypothy {
		forvalues x=1/`maxpreg' {

			use "$Tempdatadir\codes_`var'_all", clear
			keep patid `var' `var'_dx_date
			merge 1:1 patid using "$Datadir\derived_data\pregnancy_cohort_final_`x'", keep(match) nogen
			keep patid `var' `var'_dx_date pregstart_num pregid
			save "$Tempdatadir\codes_`var'_`x'", replace

		}
	}
	
	foreach var in asthma ckd diab hypbp hypothy {
		
		use "$Tempdatadir\codes_`var'_1", clear

		forvalues x=2/`maxpreg' {

			append using "$Tempdatadir\codes_`var'_`x'"
	
		}

		count
		drop if `var'_dx_date>pregstart_num /*drop if 1st Dx is after pregstart date*/
		keep patid pregid `var' `var'_dx_date

		count
		save "$Tempdatadir\final_`var'", replace

	}
	
	use "$Tempdatadir\final_asthma", clear
	merge 1:1 patid pregid using "$Datadir\derived_data\pregnancy_cohort_final.dta", nogen
	merge 1:1 patid pregid using "$Tempdatadir\final_ckd", nogen
	merge 1:1 patid pregid using "$Tempdatadir\final_diab", nogen
	merge 1:1 patid pregid using "$Tempdatadir\final_hypbp", nogen
	merge 1:1 patid pregid using "$Tempdatadir\final_hypothy", nogen
	keep patid pregid asthma ckd diab hypbp hypothy
	replace asthma = 0 if asthma==.
	replace ckd = 0 if ckd ==. 
	replace diab = 0 if diab==.
	replace hypbp = 0 if hypbp==.
	replace hypothy = 0 if hypothy==.
	save "$Datadir\covariates\long_term_health_con_final", replace
	
*******************************************************************************

* Delete unnecessary datasets

	foreach var in asthma ckd diab hypbp hypothy {
		forvalues x=1/`maxpreg' {
			
			erase "$Tempdatadir\codes_`var'_`x'.dta"
			
		}
	}
	
	foreach var in asthma ckd diab hypbp hypothy {
		
		erase "$Tempdatadir\final_`var'.dta"
		
	}

*******************************************************************************
