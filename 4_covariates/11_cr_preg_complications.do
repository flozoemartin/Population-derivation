*******************************************************************************

* Antenatal haemorrhage, fetal growth restriction and preeclampsia during pregnancy (as a predictor of stillbirth for propensity scores) - READ codes

* Author: Harriet Forbes (adapted by Flo Martin)

* Date: 26/09/2022

*******************************************************************************

* Datasets generated by this do-file

*  - $Datadir\covariates\preg_complications_final.dta

*******************************************************************************

	use "$Datadir\derived_data\pregnancy_cohort_final.dta", clear
	bysort patid: gen bign = _N
	summ bign // n = 14 max number of pregnancies
	local maxpreg = r(max)
	sort patid pregstart_num
	drop bign

*******************************************************************************

* PRIMARY CARE	
	
* Identify fetal growth restriction, antenatal haemorrhage and preeclampsia during pregnancy for propensity score generate

	* append code lists

	use "$Codesdir\preg_complications_codelist.dta", replace
	drop if marker==1
	
	duplicates list medcode readcode // n=0
	
	keep medcode ah fgr preeclam
	save "$Tempdatadir\all_codes_preg_complications.dta", replace
	
* Identify fetal growth restriction, antenatal haemorrhage and preeclampsia records at cohort entry

	* Lift all events relating to pregnancy complications from Clinical and referral Files
	
	/*use "$Datadir\formatted_cprd_data\All_Clinical.dta", clear
	append using "$Datadir\formatted_cprd_data\All_Referral.dta"
	keep patid medcode eventdate_num /*to reduce dataset size*/
	compress
	*drop if medcode<78
	save "$Tempdatadir\clinical_reduced.dta", replace*/
	
	foreach var in ah fgr preeclam {

		use "$Tempdatadir\all_codes_preg_complications.dta", clear 
		keep if `var'==1 
		keep medcode `var'
		duplicates drop

		merge 1:m medcode using "$Tempdatadir\clinical_reduced.dta", keep(match) nogen
		rename eventdate `var'_dx_date
		drop if `var'_dx_date==.
		keep patid `var'_dx_date `var'
		save "$Tempdatadir\codes_`var'_all", replace

	}
	
	foreach var in ah fgr preeclam {
		forvalues x=1/`maxpreg' {

			use "$Tempdatadir\codes_`var'_all", clear
			keep patid `var' `var'_dx_date
			merge m:1 patid using "$Datadir\derived_data\pregnancy_cohort_final_`x'", keep(match) nogen
			keep if `var'_dx_date>=pregstart_num & `var'_dx_date<pregend_num
			keep patid `var' `var'_dx_date pregstart_num pregid
			save "$Tempdatadir\codes_`var'_`x'", replace

		}
	}
	
	foreach var in ah fgr preeclam {
		
		use "$Tempdatadir\codes_`var'_1", clear

		forvalues x=2/`maxpreg' {

			append using "$Tempdatadir\codes_`var'_`x'"
	
		}

		count
		duplicates report
		duplicates drop patid pregid, force
		keep patid pregid `var' `var'_dx_date

		count
		save "$Tempdatadir\final_`var'", replace

	}
	
*******************************************************************************

* SECONDARY CARE

	use "$Codesdir\ICDCode_pre_eclampsia_signed_off_DR.dta", clear
	gen preeclam=1
	
	merge 1:1 code using "$Codesdir\ICD10_preg_complications_codelist.dta", nogen

	drop if marker==1
	keep code ah fgr preeclam
	rename code icd 
	save "$Tempdatadir\ICD10_preg_complications_codelist.dta", replace
	
	foreach var in ah fgr preeclam {

		use "$Tempdatadir\ICD10_preg_complications_codelist.dta", clear 
		keep if `var'==1 
		keep icd `var'
		duplicates drop

		merge 1:m icd using "$Datadir\formatted_linked_data\hes_diagnosis_epi", keep(match) nogen
		rename epistart_num `var'_dx_date
		drop if `var'_dx_date==.
		keep patid `var'_dx_date `var'
		save "$Tempdatadir\codes_`var'_all", replace

	}
	
	use "$Datadir\derived_data\pregnancy_cohort_final.dta", clear
	bysort patid: gen bign = _N
	summ bign // n = 14 max number of pregnancies
	local maxpreg = r(max)
	sort patid pregstart_num
	drop bign
	
	foreach var in ah fgr preeclam {
		forvalues x=1/`maxpreg' {

			use "$Tempdatadir\codes_`var'_all", clear
			keep patid `var' `var'_dx_date
			merge m:1 patid using "$Datadir\derived_data\pregnancy_cohort_final_`x'", keep(match) nogen
			keep if `var'_dx_date>=pregstart_num & `var'_dx_date<pregend_num
			keep patid `var' `var'_dx_date pregstart_num pregid
			save "$Tempdatadir\codes_`var'_`x'", replace

		}
	}
	
	foreach var in ah fgr preeclam {
		
		use "$Tempdatadir\codes_`var'_1", clear

		forvalues x=2/`maxpreg' {

			append using "$Tempdatadir\codes_`var'_`x'"
	
		}

		count
		duplicates report
		duplicates drop patid pregid, force
		keep patid pregid `var' `var'_dx_date

		count
		save "$Tempdatadir\icd_final_`var'", replace

	}
	
*******************************************************************************

* Merge primary care and secondary care information together
	
	use "$Tempdatadir\final_ah", clear
	merge 1:1 patid pregid using "$Datadir\derived_data\pregnancy_cohort_final.dta", nogen
	merge 1:1 patid pregid using "$Tempdatadir\final_fgr", nogen
	merge 1:1 patid pregid using "$Tempdatadir\final_preeclam", nogen
	merge 1:1 patid pregid using "$Tempdatadir\icd_final_ah", update replace nogen
	merge 1:1 patid pregid using "$Tempdatadir\icd_final_fgr", update replace nogen
	merge 1:1 patid pregid using "$Tempdatadir\icd_final_preeclam", update replace nogen
	keep patid pregid ah fgr preeclam
	replace ah = 0 if ah==.
	replace fgr = 0 if fgr==. 
	replace preeclam = 0 if preeclam==.
	save "$Datadir\covariates\preg_complications_final", replace
	
*******************************************************************************

* Delete unnecessary datasets

	foreach var in ah fgr preeclam {
		forvalues x=1/`maxpreg' {
			
			erase "$Tempdatadir\codes_`var'_`x'.dta"
			
		}
	}
	
	foreach var in ah fgr preeclam {
		
		erase "$Tempdatadir\final_`var'.dta"
		
	}
	
	foreach var in ah fgr preeclam {
		
		erase "$Tempdatadir\icd_final_`var'.dta"
		
	}

*******************************************************************************
