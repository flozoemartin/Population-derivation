*******************************************************************************

* Maternal endometriosis and polycystic ovary syndrome

* Author: Harriet Forbes (adapted by Flo Martin)

* Date: 30/09/2023

*******************************************************************************

* Datasets generated by this do-file

*  - $Datadir\covariates\gynae_problems_final.dta

*******************************************************************************

	use "$Datadir\derived_data\pregnancy_cohort_final.dta", clear
	bysort patid: gen bign = _N
	summ bign // n = 14 max number of pregnancies
	local maxpreg = r(max)
	sort patid pregstart_num
	drop bign
	
*******************************************************************************

* PRIMARY CARE

* Identify endometriosis and polycystic ovary syndrome at pregnancy start

	* append code lists

	use "$Codesdir\endo_pcos_codelist.dta", replace
	
	duplicates list medcode readcode // n=0
	
	keep medcode endo pcos
	save "$Tempdatadir\all_codes_gynae.dta", replace
	
* Identify endometriosis and polycystic ovary syndrome records at cohort entry

	* Lift all events relating to ASD from Clinical and referral Files
	
	/*use "$Datadir\formatted_cprd_data\All_Clinical.dta", clear
	append using "$Datadir\formatted_cprd_data\All_Referral.dta"
	keep patid medcode eventdate_num /*to reduce dataset size*/
	compress
	drop if medcode<78
	save "$Tempdatadir\clinical_reduced.dta", replace*/
	
	foreach var in endo pcos {

		use "$Tempdatadir\all_codes_gynae.dta", clear 
		keep if `var'==1 
		keep medcode `var'
		duplicates drop

		merge 1:m medcode using "$Tempdatadir\clinical_reduced.dta", keep(match) nogen
		rename eventdate `var'_dx_date
		drop if `var'_dx_date==.
		keep patid `var'_dx_date `var'
		bysort patid (`var'_dx_date): keep if _n==1 /*keeps earliest diagnosis: create 1 record per patient*/
		save "$Tempdatadir\codes_`var'_all", replace

	}
	
	foreach var in endo pcos {
		forvalues x=1/`maxpreg' {

			use "$Tempdatadir\codes_`var'_all", clear
			keep patid `var' `var'_dx_date
			merge 1:1 patid using "$Datadir\derived_data\pregnancy_cohort_final_`x'", keep(match) nogen
			keep patid `var' `var'_dx_date pregstart_num pregid
			save "$Tempdatadir\codes_`var'_`x'", replace

		}
	}
	
	foreach var in endo pcos {
		
		use "$Tempdatadir\codes_`var'_1", clear

		forvalues x=2/`maxpreg' {

			append using "$Tempdatadir\codes_`var'_`x'"
	
		}

		count
		drop if `var'_dx_date>pregstart_num /*drop if 1st Dx is after pregstart date*/
		keep patid pregid `var' `var'_dx_date

		count
		save "$Tempdatadir\final_`var'", replace

	}
	
*******************************************************************************

* SECONDARY CARE

	use "$Datadir\derived_data\pregnancy_cohort_final.dta", clear
	bysort patid: gen bign = _N
	summ bign // n = 14 max number of pregnancies
	local maxpreg = r(max)
	sort patid pregstart_num
	drop bign

	use "$Codesdir\ICD10_gynae_problems_codelist.dta", clear

	drop if marker==1
	keep code endo pcos
	rename code icd 
	save "$Tempdatadir\ICD10_gynae_problems_codelist.dta", replace
	
	foreach var in endo pcos {

		use "$Tempdatadir\ICD10_gynae_problems_codelist.dta", clear 
		keep if `var'==1 
		keep icd `var'
		duplicates drop

		merge 1:m icd using "$Datadir\formatted_linked_data\hes_diagnosis_epi", keep(match) nogen
		rename epistart_num `var'_dx_date
		drop if `var'_dx_date==.
		keep patid `var'_dx_date `var'
		bysort patid (`var'_dx_date): keep if _n==1 /*keeps earliest diagnosis: create 1 record per patient*/
		save "$Tempdatadir\codes_`var'_all", replace

	}
	
	foreach var in endo pcos {
		forvalues x=1/`maxpreg' {

			use "$Tempdatadir\codes_`var'_all", clear
			keep patid `var' `var'_dx_date
			merge 1:1 patid using "$Datadir\derived_data\pregnancy_cohort_final_`x'", keep(match) nogen
			keep patid `var' `var'_dx_date pregstart_num pregid
			save "$Tempdatadir\codes_`var'_`x'", replace

		}
	}
	
	foreach var in endo pcos {
		
		use "$Tempdatadir\codes_`var'_1", clear

		forvalues x=2/`maxpreg' {

			append using "$Tempdatadir\codes_`var'_`x'"
	
		}

		count
		drop if `var'_dx_date>pregstart_num /*drop if 1st Dx is after pregstart date*/
		keep patid pregid `var' `var'_dx_date

		count
		save "$Tempdatadir\icd_final_`var'", replace

	}
	
*******************************************************************************

* Merge all the datasets together

	use "$Tempdatadir\final_endo", clear
	merge 1:1 patid pregid using "$Datadir\derived_data\pregnancy_cohort_final.dta", nogen
	merge 1:1 patid pregid using "$Tempdatadir\final_pcos", nogen
	merge 1:1 patid pregid using "$Tempdatadir\icd_final_pcos", nogen update replace
	merge 1:1 patid pregid using "$Tempdatadir\icd_final_endo", nogen update replace
	keep patid pregid endo pcos
	replace endo = 0 if endo==.
	replace pcos = 0 if pcos ==. 
	save "$Datadir\covariates\gynae_problems_final", replace
	
*******************************************************************************

* Delete unnecessary datasets

	foreach var in endo pcos {
		forvalues x=1/`maxpreg' {
			
			erase "$Tempdatadir\codes_`var'_`x'.dta"
			
		}
	}
	
	foreach var in endo pcos {
		
		erase "$Tempdatadir\final_`var'.dta"
		
	}

*******************************************************************************
