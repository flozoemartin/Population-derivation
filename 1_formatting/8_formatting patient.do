********************************************************************************

* This syntax routine pulls together all the pieces of Patient data provided by CPRD in the new data delivery ready for using with the old Patient data delivery - All_Patient as these data are small enough to use as one dataset (unlike Therapy)

* Author: Flo Martin (adapted from scripts by Hein Heuvelman)

* Date: 27/06/2023

********************************************************************************

* Datasets generated by this do-file

	* All Patient data for using in subsequent PhD projects
	
	* $Tempdatadir\All_Patient.dta 

********************************************************************************

* Start logging

	log using "$Logdir\1_formatting\8_formatting patient.txt", replace
	
********************************************************************************

* Pull together pieces of data within each Patient chunk

	use "$Rawdatadir\redelivery may 23\primary care\Patient_01_0.dta", clear
	
	forvalues x=1/10 {	
		
		append using "$Rawdatadir\redelivery may 23\primary care\Patient_01_`x'.dta"
		
	}
	
	append using "$Cohortonedir\formatted_cprd_data\All_Patient.dta", gen(sept_mum)
	append using "$Cohorttwodir\formatted_cprd_data\All_Patient.dta", gen(sept_baby)
	count
	
	label define sept_lb 0"June record" 1"Sept record"
	label values sept_mum sept_lb
	label values sept_baby sept_lb
	
	drop *_num dep dia hyt thy
	
	* Create numerical variables for eventdate and sysdate
		
		gen chsdate_num = date(chsdate, "DMY")
		format chsdate_num %td
			
		gen frd_num = date(frd, "DMY")
		format frd_num %td
			
		gen crd_num = date(crd, "DMY")
		format crd_num %td
			
		gen tod_num = date(tod, "DMY")
		format tod_num %td
		
	* Label variables
			
		label variable patid "Patient ID"
		label variable vmid "Old VM ID for patient when practice used the VAMP system"
		label variable gender "Patient's gender"
		label variable yob "Patient's year of birth"
		label variable mob "Patient's month of birth"
		label variable marital "Patients current marital status (lookup MAR)"
		label variable famnum "Family ID"
		label variable chsreg "Patient registered with Child Health Surveillance"
		label variable chsdate "Date of registration with CHS - string"
		label variable chsdate_num "Date of registration with CHS - numerical"
		label variable prescr "Type of prescribing exception the patient has currently (medical, maternity)"
		label variable capsup "Level of capitation supplement the patient has currently (low, medium, high)"
		label variable frd "Date of patient's first registation with practice (important: see data spec for info)"
		label variable frd_num "Date of patient's first registration - numerical"
		label variable crd "Date the patients current registration with practice began (important: see data spec for info)"
		label variable crd_num "Date the patient's current registration with practice began - numerical"
		label variable regstat "Status of registration detailing gaps and temporary patients"
		label variable reggap "Number of days missing in the patients' registration details"
		label variable internal "Number of internal transfer out periods, in the patient's registration details"
		label variable tod "Date patient transferred out of practice, if relevant"
		label variable tod_num "Date patient tranferred out of practice - numerical"
		label variable toreason "Reason for patient transferring out of practice"
		label variable deathdate "Date of death of patient - derived using a CPRD algorithm"
		label variable accept "Flag to indicate whether patient has met certain quality standards"
			
		duplicates drop
		
		* Duplicates to be tagged and dropped where babies appear in both All_Patient cohort one and All_Patient in cohort two 
		
		duplicates tag patid, gen(tag)
		tab tag
		sort patid
		br if tag==1
		
		drop if tag==1 & sept_mum==1
		
		drop tag
		duplicates tag patid, gen(tag)
		tab tag
		sort patid
		br if tag==1
		
		drop if tag==1 & sept_baby==1
		
	* Compress data
		
		compress *
		
	count
	
	save "$Datadir\formatted_cprd_data\All_Patient.dta", replace
	
********************************************************************************

* Identify patient's to drop based on build differences

	use "$Datadir\formatted_cprd_data\All_Patient.dta", clear
	
	keep patid
	duplicates drop
	count
	
	merge 1:1 patid using "$Tempdatadir\builds.dta", keep(3) nogen
	
	count
	
	tab different_builds, nol m
	
	keep if different_builds==3 // drop those who withdrew consent between June and Sept (n=107) and new patients (not in June but in Sept n=1)
	
	save "$Tempdatadir\patients to keep.dta", replace
	
	use "$Datadir\formatted_cprd_data\All_Patient.dta", clear
	
	merge 1:1 patid using "$Tempdatadir\patients to keep.dta", keep(3) nogen
	
	save "$Datadir\formatted_cprd_data\All_Patient.dta", replace
	
********************************************************************************

* Stop logging

	log close
	
********************************************************************************
