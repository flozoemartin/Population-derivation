********************************************************************************

* Format linked Hospital Episode Statistics (HES) data (https://digital.nhs.uk/data-and-information/data-tools-and-services/data-services/hospital-episode-statistics/hospital-episode-statistics-data-dictionary)

* Author: Paul Madley-Dowd (adapted for antidepressant project by Flo Martin)

* Date: 12/05/2023

********************************************************************************

* Datasets generated by this do-file

*	- $Datadir\formatted_linked_data\hes_diagnosis_epi.dta
*	- $Datadir\formatted_linked_data\hes_episodes.dta
* 	- $Datadir\formatted_linked_data\hes_hospital.dta
* 	- $Datadir\formatted_linked_data\hes_maternity.dta
* 	- $Datadir\formatted_linked_data\hes_patient.dta
* 	- $Datadir\formatted_linked_data\hes_procedures.dta
* 	- $Datadir\formatted_linked_data\hes_death.dta
* 	- $Datadir\formatted_linked_data\linkage_eligibility.dta

********************************************************************************

* Start logging

	log using "$Logdir\1_formatting\4_formatting hes.txt", replace
	
********************************************************************************

* HES Admitted Patient Care (APC) - includes HES critical care, HES diagnosis, HES hospitalisations, HES maternity, HES patient, HES procedure 

	* HES diagnosis epi
	
		use "$Rawdatadir\original sept 21\linkage\stata\hes_diagnosis_epi.dta", clear
		append using "$Rawdatadir\redelivery may 23\linkage\stata\hes_diagnosis_epi_23.dta"
		
		duplicates drop
		count	// n = 41,696,506 diagnoses...
		codebook patid // ... among 1,982,356 patients

		* Generate numeric versions of date variables
		gen epistart_num = date(epistart, "DMY")
		format %td epistart_num

		* Label variables
		label variable patid "Patient ID"
		label variable spno "Number uniquely identifying a hospitalisation"
		label variable epikey "Episode key uniquely identifying an episode of care"
		label variable epistart "Start date of episode of care"
		label variable epistart_num "Start date of episode of care (numeric)"
		label variable icd "An ICD10 diagnosis code in XXX or XXX.X format"
		label variable icdx "5th/6th characters of the ICD code (if available)"
		label variable d_order "Ordering of diagnosis code in episode"

		compress
		save "$Datadir\formatted_linked_data\hes_diagnosis_epi.dta", replace
		
	* HES diagnosis hosp
	
		use "$Rawdatadir\original sept 21\linkage\stata\hes_diagnosis_hosp.dta", clear
		append using "$Rawdatadir\redelivery may 23\linkage\stata\hes_diagnosis_hosp_23.dta"
		
		duplicates drop
		count	// n = 35,978,227 diagnoses...
		codebook patid // ... among 1,982,356 patients

		* Generate numeric versions of date variables
		gen admidate_num = date(admidate, "DMY")
		format %td admidate_num
		
		gen discharged_num = date(discharged, "DMY")
		format %td discharged_num

		* Label variables
		label variable patid "Patient ID"
		label variable spno "Number uniquely identifying a hospitalisation"
		label variable admidate "Admission date"
		label variable discharged "Discharge date"
		label variable icd "An ICD10 diagnosis code in XXX or XXX.X format"
		label variable icdx "5th/6th characters of the ICD code (if available)"

		compress
		save "$Datadir\formatted_linked_data\hes_diagnosis_hosp.dta", replace
	
	* HES episodes
	
		use "$Rawdatadir\original sept 21\linkage\stata\hes_episodes.dta", clear
		gen sept=1
		append using "$Rawdatadir\redelivery may 23\linkage\stata\hes_episodes_23.dta"
		replace sept=0 if sept==.
		
		duplicates drop
		count	// n = 12,961,095 episodes...
		codebook patid 	// ...among 1,982,356 patients
	
		* Generate numeric versions of date variables
		gen epistart_num = date(epistart, "DMY")
		gen admidate_num = date(admidate, "DMY")
		format %td epistart_num admidate_num

		* Label variables
		label variable patid "Patient ID"
		label variable spno "Number uniquely identifying a hospitalisation"
		label variable epikey "Episode key uniquely identifying an episode of care"
		label variable epistart "Start date of episode of care"
		label variable epistart_num "Start date of episode of care (numeric)"
		label variable epitype "Type of episode - (1) General episode (2) delivery episode (3) birth episode"
		label variable eorder "Order of episode within spell"
		label variable admidate "Date of admission"
		label variable admidate_num "Date of admission (numeric)"
		label variable admimeth "Method of admission"
		
		tab epitype
		tab admimeth 
		/* 	82: Other: babies born in health care provider 
			83: Other: babies born outside the health care provider, except when born at home as intended
		*/
		
		duplicates tag patid spno epikey epistart_num, gen(tag)
		tab tag
		
		sort patid spno epikey epistart_num
		br patid spno epikey epistart_num sept if tag==1

		egen flag = rowmiss(patid spno epikey admidate epistart epiend discharged eorder epidur epitype admimeth admisorc disdest dismeth mainspef tretspef pconsult intmanig classpat firstreg ethnos epistart_num admidate_num)
		
		br patid spno epikey epistart_num flag sept if tag==1
		
		drop if tag==1 & sept==1
		
		compress
		save "$Datadir\formatted_linked_data\hes_episodes.dta", replace
		
	* HES hospitalisations
	
		use "$Rawdatadir\original sept 21\linkage\stata\hes_hospital.dta", clear
		append using "$Rawdatadir\redelivery may 23\linkage\stata\hes_hospital_23.dta"
		
		duplicates drop
		count // n = 11,772,402 hospitalisations...
		codebook patid 	// ... among 1,982,356 patients
		
		* Generate numeric versions of date variables
		gen admidate_num = date(admidate, "DMY")
		format %td admidate_num

		*Label variables
		label variable patid "Patient ID"
		label variable spno "Number uniquely identifying a hospitalisation"
		label variable admidate "Date of admission"
		label variable admidate_num "Date of admission (numeric)"
		label variable duration "Duration of hospitalisation spell in days"

		compress
		save "$Datadir\formatted_linked_data\hes_hospital.dta", replace
		
	* HES maternity
	
		use "$Rawdatadir\original sept 21\linkage\stata\hes_maternity.dta", clear
		append using "$Rawdatadir\redelivery may 23\linkage\stata\hes_maternity_23.dta"
		
		duplicates drop
		count	// n = 5,622,252 records...
		codebook patid // ...among 1,284,658 patients
		
		* Generate numeric versions of date variables
		gen epistart_num = date(epistart, "DMY")
		gen epiend_num = date(epiend, "DMY")
		gen anasdate_num = date(anasdate, "DMY")
		format %td epistart_num epiend_num anasdate_num

		* Format variables 
		encode numbaby, gen(numbaby_num)
		tab numbaby_num, nol
		recode numbaby_num (7=.) (8=.) (9=.) 
		label define lb_numbaby /* 
		*/ 1  "One" /*  
		*/ 2  "Two"  /*
		*/ 3  "Three"  /*
		*/ 4  "Four"  /*
		*/ 5  "Five"  /*
		*/ 6  "Six or more"
		label values numbaby_num lb_numbaby
		tab numbaby_num

		tab neocare
		recode neocare 9=.
		label define lb_neocare /*
		*/ 0 "Normal care" 		/* care given by the mother or mother substitute, with medical and neonatal nursing advice if needed  
		*/ 1 "Special care" 	/* care given in a special nursery, transitional care ward or postnatal ward, which provides care and treatment exceeding normal routine care. Some aspects of special care can be undertaken by a mother supervised by qualified nursing staff. Special nursing care includes support for and education of the infant's parents  
		*/ 2  "Level 2 intensive care" /* (high dependency intensive care): care given in an intensive or special care nursery, which provides continuous skilled supervision by qualified and specially trained nursing staff who may care for more babies than in level 1 intensive care. Care includes support for the infant's parents  
		*/ 3  "Level 1 intensive care" /* (maximal intensive care): care given in an intensive or special care nursery, which provides continuous skilled supervision by qualified and specially trained nursing and medical staff. Care includes support for the infant's parents  
		/* 4 "" */ Not in data dictionary
		*/ 8  "Not applicable" 	/* the episode of care does not involve a neonate at any time  
		*/ 
		label values neocare lb_neocare
		tab neocare

		encode birordr, gen(birordr_num)
		tab birordr_num, nol
		recode birordr_num (8=.) (9=.) (10=8) 
		label define lb_birordr /* 
		*/ 8 "Not applicable"
		label values birordr_num lb_birordr
		tab birordr_num

		tab birstat
		recode birstat 8=. 9=.
		label define lb_birstat /*
		*/ 1 "Live" /*  
		*/ 2 "Still birth: ante-partum" /*  
		*/ 3 "Still birth: intra-partum"  /* 
		*/ 4 "Still birth: indeterminate" 
		label values birstat lb_birstat
		tab birstat

		tab biresus
		recode biresus 9=.
		label define lb_biresus /* 
		*/ 1 "Positive pressure nil, drugs nil" /*  
		*/ 2 "Positive pressure nil, drugs administered" /* 
		*/ 3 "Positive pressure by mask, drugs nil" /*  
		*/ 4 "Positive pressure by mask, drugs administered" /*  
		*/ 5 "Positive pressure by endotracheal tube, drugs nil" /*  
		*/ 6 "Positive pressure by endotracheal tube, drugs administered" /*  
		*/ 8 "Not applicable (e.g. stillborn, where no method of resuscitation was attempted)" 
		label values biresus lb_biresus
		tab biresus

		recode birweit (9999=.) (0=.) // data dictionary specifies values betwee 10g and 6999g - HF to check whether this recoding is sensible - could also recode 0 to a not known value
		recode birweit -1=.

		tab delmeth
		encode delmeth, gen(delmeth_num)
		recode delmeth_num (11=.) (1=0) (2=1) (3=2) (4=3) (5=4) (6=5) (7=6) (8=7) (9=8) (10=9)
		label define lb_delmeth /*
		*/ 0 "Spontaneous vertex" /* (normal vaginal delivery, occipitoanterior)  
		*/ 1 "Spontaneous other cephalic" /* (cephalic vaginal delivery with abnormal presentation of head at delivery, without instruments, with or without manipulation)  
		*/ 2 "Low forceps, not breech" /* including forceps delivery not otherwise specified (forceps, low application, without manipulation)  
		*/ 3 "Other forceps, not breech" /* including high forceps and mid forceps (forceps with manipulation)  
		*/ 4 "Ventouse, vacuum extraction" /* 
		*/ 5 "Breech, including partial breech extraction" /* (spontaneous delivery assisted or unspecified)  
		*/ 6 "Breech" /*
		*/ 7 "Elective caesarean section" /*
		*/ 8 "Emergency caesarean section" /*
		*/ 9 "Other" 
		label values delmeth_num lb_delmeth
		tab delmeth_num

		tab delonset
		recode delonset 9=.
		label define lb_delonset /*
		*/ 1 "Spontaneous" /* the onset of regular contractions whether or not preceded by spontaneous rupture of the membranes  
		*/ 2 "Any caesarean section" /* carried out immediately following the onset of labour, when the decision was made before labour  
		*/ 3 "Surgical induction by amniotomy" /*  
		*/ 4 "Medical induction" /*, including the administration of agents either orally, intravenously or intravaginally with the intention of initiating labour  
		*/ 5 "Combination of surgical induction and medical induction" /*  
		*/ 8 "Not applicable" 
		label values delonset lb_delonset
		tab delonset

		summ gestat
		recode gestat (99=.)  

		tab numpreg
		recode numpreg (99=.)
		
		tab delprean
		recode delprean 9=.
		label define lb_delprean /*
		*/ 1 "General anaesthetic: the administration by a doctor of an agent to produce unconsciousness" /* 
		*/ 2 "Epidural or caudal anaesthetic: the injection of a local anaesthetic into the epidural space" /* carried out immediately following the onset of labour, when the decision was made before labour  
		*/ 3 "Spinal anaesthetic: the injection of a local anaesthetic agent into the subarachnoid space " /*  
		*/ 4 "General anaesthetic and epidural or caudal anaesthetic" /*, including the administration of agents either orally, intravenously or intravaginally with the intention of initiating labour  
		*/ 5 "General anaesthetic and spinal anaesthetic" /*  
		*/ 6 "Epidural or caudal, and spinal anaesthetic" /* 
		*/ 7 "Other than 1 to 6" /* 
		*/ 8 "Not applicable"
		lab values delprean lb_delprean
		tab delprean
		
		tab delposan
		recode delposan 9=.
		label values delposan lb_delprean
		tab delposan
		
		sum antedur
		
		tab delchang
		recode delchang 9=.
		label define lb_delchang /*
		*/ 1 "Decision made during pregnancy because the patient's address changed " /* 
		*/ 2 "Decision made during pregnancy for clinical reasons" /*
		*/ 3 "Decision made during pregnancy for other reasons" /*  
		*/ 4 "Decision made during labour for clinical reasons " /*  
		*/ 5 "Decision made during labour for other reasons " /*  
		*/ 6 "Occurred unintentionally during labour" /* 
		*/ 8 "Not applicable"
		label values delchang lb_delchang
		tab delchang
		
		tab delplac
		recode delplac 9=.
		label define lb_delplac /*
		*/ 0 "In NHS hospital: delivery facilities associated with midwife ward" /*
		*/ 1 "At a domestic address" /* 
		*/ 2 "In NHS hospital: delivery facilities associated with consultant ward" /*
		*/ 3 "In NHS hospital: delivery facilities associated with General Medical Practitioner ward" /*  
		*/ 4 "In NHS hospital: delivery facilities associated with consultant / General Medical Practitioner / midwife ward, inclusive of any combination of two of the professionals mentioned" /*
		*/ 5 "In private hospital" /*  
		*/ 6 "In other hospital or institution" /* 
		*/ 7 "In NHS hospital: ward or unit without delivery facilities" /* 
		*/ 8 "Other than those above"
		label values delplac lb_delplac
		tab delplac
		
		tab delinten
		recode delinten 9=.
		label define lb_delinten /*
		*/ 0 "In NHS hospital: delivery facilities associated with midwife ward" /*
		*/ 1 "At a domestic address" /* 
		*/ 2 "In NHS hospital: delivery facilities associated with consultant ward" /*
		*/ 3 "In NHS hospital: delivery facilities associated with General Medical Practitioner ward" /*  
		*/ 4 "In NHS hospital: delivery facilities associated with consultant / General Medical Practitioner / midwife ward, inclusive of any combination of two of the professionals mentioned" /*
		*/ 5 "In private hospital" /*  
		*/ 6 "In other hospital or institution" /* 
		*/ 7 "In NHS hospital: ward or unit without delivery facilities" /* 
		*/ 8 "None of the above"
		label values delinten lb_delinten
		tab delinten
		
		tab anagest
		
		summ matage
		
		sum postdur
		
		tab sexbaby
		replace sexbaby ="" if sexbaby=="0" | sexbaby=="4" | sexbaby=="5" | sexbaby=="6" | sexbaby=="7" | sexbaby=="8" | sexbaby=="9" | sexbaby=="X"
		replace sexbaby = "1" if sexbaby=="M"
		replace sexbaby = "2" if sexbaby=="F"
		tab sexbaby
		gen sexbaby_num = real(sexbaby)
		label define lb_sexbaby 1"Male" 2"Female" 3"Indeterminate"
		label value sexbaby_num lb_sexbaby
		tab sexbaby_num
		
		tab delstat
		recode delstat 9=.
		label define lb_delstat /*
		*/ 1 "Hospital doctor" /* 
		*/ 2 "General medical practitioner" /*
		*/ 3 "Midwife" /*  
		*/ 8 "Other than above" 
		label values delstat lb_delstat
		tab delstat
		
		* Label variables
		label variable patid "Patient ID"
		label variable spno "Number uniquely identifying a hospitalisation"
		label variable epikey "Episode key uniquely identifying an episode of care"
		label variable epistart "Date of start of episode"
		label variable epistart_num "Start date of episode of care (numeric)"
		label variable epiend "Date of end of episode"
		label variable epiend_num "Date of end of episode (numeric)"
		label variable eorder "Order of episode within spell"
		label variable epidur "Duration of episode in days"
		label variable numbaby "Number of babies delivered at the end of a single pregnancy"
		label variable numbaby_num "Number of babies delivered at the end of a single pregnancy (numeric)"
		label variable numtailb "Number of baby tails"
		label variable matordr "Order of birth"
		label variable neocare "Neonatal level of care"
		label variable anasdate "First antenatal assessment date"
		label variable anasdate_num "First antenatal assessment date (numeric)"
		label variable birordr "The position in the sequence of births"
		label variable birordr_num "The position in the sequence of births (numeric)"
		label variable birstat "Indicates whether the baby was born alive or dead (still birth)"
		label variable biresus "Identifies resuscitation method used to get the baby breathing"
		label variable sexbaby "Sex of baby"
		label variable birweit "Weight of the baby in grams immediately after birth"
		label variable delmeth "Method used to deliver a baby that is a registrable birth"
		label variable delmeth_num "Method used to deliver a baby that is a registrable birth (numeric)"
		label variable delonset "Method used to induce (initiate) labour, rather than to accelerate it"
		label variable gestat "Length of gestation - number of completed weeks of gestation"
		label variable numpreg "Number of previous pregnancies that resulted in a registered birth (live or still born)"
		label variable antedur "Antenatal days of stay"
		label variable postdur "Postnatal days of stay"

		order patid* spno* epikey* epistart* epiend* eorder* epidur* numbaby* numtailb* matordr* neocare* anasdate* birordr* birstat* biresus* sexbaby* birweit* delmeth* delonset* gestat* numpreg* antedur* postdur*

		compress
		save "$Datadir\formatted_linked_data\hes_maternity.dta", replace
		
	* HES patient
	
		use "$Rawdatadir\original sept 21\linkage\stata\hes_patient.dta", clear
		append using "$Rawdatadir\redelivery may 23\linkage\stata\hes_patient_23.dta"
		
		duplicates drop
		count
		codebook patid // n = 1,983,854 patients
		
		* Generate numeric versions of date variables
		label variable patid "Patient ID"
		label variable pracid "Practice ID"
		label variable gen_ethnicity "Ethnicity derived from HES data (including Admitted patient care, Outpatient, A&E, PROMs and DID) "

		compress
		save "$Datadir\formatted_linked_data\hes_patient.dta", replace
		
	* HES procedures 
	
		use "$Rawdatadir\original sept 21\linkage\stata\hes_procedures.dta", clear
		append using "$Rawdatadir\redelivery may 23\linkage\stata\hes_procedures_23.dta"
		
		duplicates drop
		count 	// n = 17,850,176 procedures...
		codebook patid // ...among 1,582,210 patients
		
		* Generate numeric versions of date variables
		gen epistart_num = date(epistart, "DMY")
		gen admidate_num = date(admidate, "DMY")
		gen evdate_num = date(evdate, "DMY")
		format %td epistart_num admidate_num evdate_num

		* Label variables
		label variable patid "Patient ID"
		label variable spno "Number uniquely identifying a hospitalisation"
		label variable epikey "Episode key uniquely identifying an episode of care"
		label variable admidate "Date of admission"
		label variable admidate_num "Date of admission (numeric)"
		label variable epistart "Date of start of episode"
		label variable epistart_num "Start date of episode of care (numeric)"
		label variable opcs "An OPCS 4 procedure code"
		label variable evdate "Date of operation / procedure"
		label variable evdate_num "Date of operation / procedure (numeric)"
		label variable p_order "Ordering of OPCS code in episode, within range 1-24"

		compress
		save "$Datadir\formatted_linked_data\hes_procedures.dta", replace

	* HES death

		use "$Rawdatadir\original sept 21\linkage\stata\hes_death.dta", clear
		
		foreach x in dod_partial cause15 cause_neonatal8 {
			
			gen `x'_str = string(`x')
			drop `x'
			rename `x'_str `x'
			
		}
		
		append using "$Rawdatadir\redelivery may 23\linkage\stata\hes_death_23.dta", gen(may)
		
		duplicates tag patid, gen(dup)
		tab dup
		sort patid may
		br if dup==1
		drop if dup==1 & may==0
		count // n = 99,391 deaths recorded...
		codebook patid	// ...among 99,383 patients
		
		* Create numerical variables for date variables
		gen dor_num = date(dor, "DMY")
		format dor_num %td
		gen dod_num = date(dod, "DMY")
		format dod_num %td
		
		* Label variables
		label variable patid "Patient ID"
		label variable pracid "Practice ID"
		label variable dor_num "Date of registration - numerical"
		label variable dod_num "Date on which patient died - numerical"

		sort patid

		compress *
		save "$Datadir\formatted_linked_data\hes_death.dta", replace
		
********************************************************************************

* Linkage eligibility

	use "$Rawdatadir\original sept 21\linkage\stata\linkage_eligibility.dta", clear
	append using "$Rawdatadir\redelivery may 23\linkage\stata\linkage_eligibility_23.dta"
	
	duplicates drop 
	count 	// n = 4,722,128 patients
	
	* Generate numeric versions of date variables
	gen linkdate_num = date(linkdate, "DMY")
	
	* Label variables
	label variable patid "Patient ID"
	label variable pracid "Practice ID"
	label variable hes_apc_e "HES Admitted Patient Care linkage"
	label variable ons_death_e "ONS Death linkage"
	label variable mhds_e "MHSDS linkage"
	
	keep patid pracid linkdate linkdate_num hes_apc_e ons_death_e mhds_e
	
	compress
	save "$Datadir\formatted_linked_data\linkage_eligibility.dta", replace
	
********************************************************************************

* Stop logging

	log close
	
********************************************************************************
